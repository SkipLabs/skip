/*****************************************************************************/
/* SQL tables.*/
/*****************************************************************************/

module SKSQL;

const tableInfo: SKFS.DirName = SKFS.DirName::create("/tablesInfo/");
const views: SKFS.DirName = SKFS.DirName::create("/views/");
const subsInput: SKFS.DirName = SKFS.DirName::create("/subsInput/");
const subsOutput: SKFS.DirName = SKFS.DirName::create("/subsOutput/");
const indexInput: SKFS.DirName = SKFS.DirName::create("/indexInput/");
const indexOutput: SKFS.DirName = SKFS.DirName::create("/indexOutput/");
const indexByCol: SKFS.DirName = SKFS.DirName::create("/indexByCol/");

extension class TypeDescr extends SKFS.File

class DirDescr{
  name: String,
  colNames: Array<String>,
  schema: Array<TypeDescr>,
  kinds: Array<(Int, IKind, Type)>,
  cols: SortedMap<String, Int>,
  dirName: SKFS.DirName,
  alias: ?String,
  windowSize: ?Int,
  isInput: Bool,
} extends SKFS.File uses Orderable {
  static fun create{
    name: String,
    schema: Array<TypeDescr>,
    dirName: SKFS.DirName,
    alias: ?String,
    windowSize: ?Int,
    isInput: Bool,
  }: this {
    colNames = schema.map(x -> x.name);
    cols = SortedMap[];
    for (i => ty in schema) {
      !cols[ty.name] = i;
    };
    static{
      name,
      colNames,
      schema,
      kinds => schema.mapWithIndex((idx, ty) -> (idx, IASC(), ty.ty)),
      cols,
      dirName,
      alias,
      windowSize,
      isInput,
    }
  }
}

fun makeSelectName(id: String): SKFS.DirName {
  SKFS.DirName::create("/" + id + "/");
}

fun getDir(dirDescr: DirDescr): SKFS.EHandle<Row> {
  SKFS.EHandle(RowValues::fromFile, dirDescr.dirName)
}

fun makeDir<T: frozen>(
  fromFile: SKFS.File ~> T,
  context: mutable SKFS.Context,
  dirName: SKFS.DirName,
  make: () -> SKFS.EHandle<T>,
): SKFS.EHandle<T> {
  context.maybeGetDir(dirName) match {
  | None() -> make()
  | Some(dir) -> SKFS.EHandle(fromFile, dir.getDirName())
  };
}

fun getTableDir(context: mutable SKFS.Context): SKFS.EHandle<DirDescr> {
  makeDir(DirDescr::fromFile, context, tableInfo, () -> {
    context.mkdir(DirDescr::fromFile, tableInfo, false, Array[])
  })
}

/*****************************************************************************/
/* Views. */
/*****************************************************************************/

fun getViewsDir(context: mutable SKFS.Context): SKFS.EHandle<SelectFile> {
  makeDir(SelectFile::fromFile, context, views, () ->
    context.mkdir(SelectFile::fromFile, views, false, Array[])
  )
}

/*****************************************************************************/
/* Subscriptions. */
/*****************************************************************************/

class SubRequestFile(
  viewName: String,
  viewDirName: SKFS.DirName,
  init: Bool,
  cmd: SKFS.CmdKind,
  sessionID: Int,
  user: ?(String, String),
) extends SKFS.File

fun getSubsDirs(
  options: Options,
  context: mutable SKFS.Context,
): (SKFS.EHandle<SubRequestFile>, SKFS.EHandle<SKFS.IntFile>) {
  if (context.unsafeMaybeGetEagerDir(subsInput) is Some _) {
    input = SKFS.EHandle(SubRequestFile::fromFile, subsInput);
    output = SKFS.EHandle(SKFS.IntFile::fromFile, subsOutput);
    return (input, output)
  };
  input = context.mkdir(SubRequestFile::fromFile, subsInput, false, Array[]);
  output = input.contextWriterKeyValues(
    SKFS.IntFile::fromFile,
    context,
    subsOutput,
    (context, writer, key, values) ~> {
      request = SubRequestFile::fromFile(values[0]);
      viewName = request.viewName;
      cmd = request.cmd;
      init = request.init;
      sessionID = request.sessionID;
      tableDir = getTableDir(context);
      (edir, dirDescr) = tableDir.maybeGet(
        context,
        SKFS.SID(request.viewName),
      ) match {
      | None() ->
        views = SKSQL.getViewsDir(context);
        viewsDir = context.unsafeGetEagerDir(views.dirName);
        selectFiles = viewsDir.getArrayRaw(SKFS.SID(viewName.toString()));
        invariant(selectFiles.size() <= 1);
        if (selectFiles.size() == 0) {
          print_error(`Error: view ${viewName.toString()} doesn't exists`);
          skipExit(3)
        };
        selectFile = SKSQL.SelectFile::fromFile(selectFiles[0]);
        if (!selectFile.virtual) {
          print_error("Error: can only connect to virtual views");
          skipExit(23);
        };
        selectAst = selectFile.value;
        compiler = SKSQL.Compiler::create(
          options with {virtual => true},
          selectAst.pos,
        );
        cselect = compiler.compileSelect(context, selectAst, true);
        selectDir = SKSQL.evalSelect(context, cselect, None());
        edir = context.unsafeGetEagerDir(selectDir.dirName);
        dirDescr = Compiler::getDirDescr(
          selectAst.params,
          cselect.from,
          cselect.params,
          selectDir.dirName.toString(),
          selectDir.dirName,
        );
        (edir, dirDescr)
      | Some(dirDescr) ->
        edir = context.unsafeGetEagerDir(dirDescr.dirName);
        (edir, dirDescr)
      };
      reset = request.user match {
      | None() -> (_, _) ~> false
      | Some((userName, password)) ->
        userFile = SKSQL.UserFile::create(context, userName, password);
        (context: SKFS.Context, tick: SKFS.Tick) ~> {
          readers = context.unsafeGetEagerDir(SKFS.accessReaders);
          for (rowKey in readers.getChangesAfter(tick)) {
            rowKey match {
            | RowKey(row, _) ->
              if (row.getInt(1).fromSome() == userFile.id) {
                return true
              }
            | _ -> invariant_violation("Unexpected key type")
            }
          };
          false
        }
      };
      (userFile, matchesGroup, filter) = request.user match {
      | None() -> (None(), (_ ~> 0), (_ ~> _ ~> true))
      | Some((userName, password)) ->
        userFile = SKSQL.UserFile::create(context, userName, password);
        accessColNbr = dirDescr.cols.maybeGet("skdb_access") match {
        | None() ->
          print_error(
            "Cannot access table: " +
              dirDescr.name +
              ".\n" +
              "You are connected as user " +
              userName +
              " you can only access tables defining the column skdb_access.",
          );
          skipExit(2)
        | Some(x) -> x
        };
        if (!(dirDescr.schema[accessColNbr].ty is INTEGER())) {
          print_error("skdb_access must be of type integer");
          skipExit(2)
        };
        (
          Some(userFile.id),
          ((file: SKFS.BaseName) ~> {
            file match {
            | k @ RowKey _ ->
              k.row.getInt(accessColNbr) match {
              | None() -> 0
              | Some(gid) -> gid
              }
            | _ -> 0
            }
          }),
          (context ~> {
            access = mutable AccessSolver(userFile.id);
            (file: SKFS.BaseName) -> {
              file match {
              | k @ RowKey _ ->
                k.row.getInt(accessColNbr) match {
                | None() -> true
                | Some(accessID) -> access.canRead(context, accessID)
                }
              | _ -> true
              }
            }
          }),
        )
      };
      context.subscribe(
        context,
        edir.dirName,
        sessionID,
        cmd,
        init,
        userFile,
        reset,
        matchesGroup,
        filter,
      );
      writer.write(key, SKFS.IntFile(sessionID))
    },
  );
  (input, output)
}

/*****************************************************************************/
/* Indexes. */
/*****************************************************************************/

extension class Index extends SKFS.File

class IndexDescr{
  name: String,
  dirName: SKFS.DirName,
  tableName: SKFS.DirName,
  fields: Array<(Int, IKind, Type)>,
  unique: Bool,
} extends SKFS.File uses Orderable {
  fun compare(y: IndexDescr): Order {
    this.name.compare(y.name)
  }
}

fun getIndexInputDir(context: mutable SKFS.Context): SKFS.EHandle<Index> {
  makeDir(Index::fromFile, context, indexInput, () ->
    context.mkdir(Index::fromFile, indexInput, false, Array[])
  )
}

fun getIndexOutputDir(
  options: Options,
  context: mutable SKFS.Context,
): SKFS.EHandle<IndexDescr> {
  input = getIndexInputDir(context);
  makeDir(IndexDescr::fromFile, context, indexOutput, () -> {
    input.contextMap(
      IndexDescr::fromFile,
      context,
      indexOutput,
      (context, index) ~> {
        createUserIndex(
          options,
          context,
          index.name,
          index.pos,
          index.fields,
          index.unique,
        )
      },
    )
  })
}

fun makeIndexEntry(tableName: SKFS.DirName, colNbr: Int): SKFS.SID {
  SKFS.SID::create(tableName.toString() + ":" + colNbr);
}

fun getIndexByColNbr(
  options: Options,
  context: mutable SKFS.Context,
): SKFS.EHandle<IndexDescr> {
  indexes = getIndexOutputDir(options, context);
  makeDir(IndexDescr::fromFile, context, indexByCol, () -> {
    indexes.contextWriterMap(
      IndexDescr::fromFile,
      context,
      indexByCol,
      (_context, writer, indexDescr) ~> {
        for (field in indexDescr.fields) {
          fieldNbr = field.i0;
          indexEntry = makeIndexEntry(indexDescr.tableName, fieldNbr);
          writer.write(indexEntry, indexDescr)
        }
      },
    )
  })
}
/*****************************************************************************/
/* Tables. */
/*****************************************************************************/

fun getTable(
  options: Options,
  context: mutable SKFS.Context,
  pos: Int,
  name: SKFS.BaseName,
): DirDescr {
  tableDir = getTableDir(context);
  tableDir.maybeGet(context, name) match {
  | None() ->
    hviews = getViewsDir(context);
    viewsDir = context.unsafeGetEagerDir(hviews.dirName);
    viewArray = viewsDir.getArrayRaw(name);
    if (viewArray.size() == 0) {
      error(pos, "Table '" + name + "' does not exist")
    } else {
      view = viewArray[0];
      selectFile = SelectFile::fromFile(view);
      dirName = SKFS.DirName::create("/" + selectFile.value.id + "/");
      compiler = SKSQL.Compiler::create(options, pos);
      cselect = compiler.compileSelect(context, selectFile.value, true);
      dirDescr = SKSQL.Compiler::getDirDescr(
        selectFile.value.params,
        cselect.from,
        cselect.params,
        cselect.id,
        dirName,
      );
      if (!selectFile.virtual) {
        _ = evalSelect(context, cselect, None());
      };
      dirDescr
    }
  | Some(dirDescr) ->
    context.getGlobal("TableMap") match {
    | None() -> dirDescr
    | Some(tableMapFile) ->
      tableMap = TableMap::fromFile(tableMapFile);
      oldDescr = tableMap.map.maybeGet(name) match {
      | None() -> dirDescr
      | Some((version, descr)) if (version == tableMap.version) -> return descr
      | Some((_, x)) -> x
      };
      oldDirName = oldDescr.dirName;
      oldDir = context.unsafeGetEagerDir(oldDirName);
      nextName = SKFS.DirName::create(
        "/next/" + dirDescr.name + "/" + tableMap.version.value + "/",
      );
      sinkName = SKFS.DirName::create(
        "/sink/" + dirDescr.name + "/" + tableMap.version.value + "/",
      );
      dir = SKFS.EagerDir{
        time => context.timeStamp(),
        dirName => nextName,
        input => true,
        fixedData => oldDir.fixedData,
        data => oldDir.data,
        totalSize => oldDir.totalSize,
        creator => context.currentArrow(),
        reallyRemove => false,
      };
      context.setDir(dir.dirName, dir);
      _ = SKFS.EagerDir::apply(
        context,
        oldDirName,
        sinkName,
        (context, _writer, key, _valueIter) ~> {
          entries = mutable Vector[];
          for (srcValue in (context.unsafeGetEagerDir(
            oldDirName,
          )).unsafeGetDataIter(key)) {
            (source, values) = srcValue;
            entries.push((key, (source.value, values)));
          };
          inputDir = context.unsafeGetEagerDir(nextName);
          inputDir.writeArraySourceMany(context, entries.iterator());
        },
        None(),
        None(),
        true,
      );
      result = dirDescr with {dirName => nextName};
      !tableMap.map[name] = (tableMap.version, result);
      context.setGlobal("TableMap", tableMap);
      result
    }
  }
}

/*****************************************************************************/
/* DUMP. */
/*****************************************************************************/

fun printDirDescr(dirDescr: DirDescr, suffix: ?String): void {
  o = print_raw;
  o("CREATE ");
  o(
    dirDescr.windowSize match {
    | None() -> "TABLE "
    | Some(0) -> "STREAM " + " "
    | Some(size) -> "WINDOW " + size + " "
    },
  );
  o(dirDescr.name);
  suffix match {
  | None() -> void
  | Some(x) -> o(x)
  };
  o(" (\n");
  for (colName in dirDescr.colNames) {
    colIndex = dirDescr.cols[colName];
    o("  ");
    o(colName);
    o(" ");
    ty = dirDescr.schema[colIndex];
    o(
      ty.ty match {
      | INTEGER() -> "INTEGER"
      | FLOAT() -> "FLOAT"
      | TEXT() -> "TEXT"
      },
    );
    if (ty.unique) {
      o(" UNIQUE");
    };
    if (ty.primary) {
      o(" PRIMARY KEY");
    };
    if (ty.notNull) {
      o(" NOT NULL");
    };
    if (colIndex < dirDescr.schema.size() - 1) {
      o(",");
    };
    o("\n");
  };
  o(");\n");
  flushStdout();
}

fun dumpTable(
  origContext: readonly SKFS.Context,
  tableName: String,
  suffix: ?String,
): void {
  context = SKFS.Context::fromSaved(origContext.clone());
  printDirDescr(getTableDir(context).get(context, SKFS.SID(tableName)), suffix);
}

fun dumpTables(origContext: readonly SKFS.Context, suffix: ?String): void {
  context = SKFS.Context::fromSaved(origContext.clone());
  handle = getTableDir(context);
  sinkName = SKFS.DirName::create("/sink_" + SKFS.genSym(0) + "/");
  _ = handle.map(handle.conv, context, sinkName, dirDescr ~> {
    if (!dirDescr.isInput) return dirDescr;
    printDirDescr(dirDescr, suffix);
    dirDescr
  });
}

fun dumpInserts(origContext: readonly SKFS.Context): void {
  context = SKFS.Context::fromSaved(origContext.clone());
  handle = getTableDir(context);
  sinkName = SKFS.DirName::create("/sink_" + SKFS.genSym(0) + "/");
  _ = handle.contextMap(handle.conv, context, sinkName, (context, dirDescr) ~> {
    if (!dirDescr.isInput) return dirDescr;
    o = print_raw;
    fileIter = context.unsafeGetEagerDir(dirDescr.dirName).unsafeGetFileIter();
    count = 1;
    o("BEGIN TRANSACTION;\n");
    for (kv in fileIter) {
      if (count % 1024 == 0) {
        o("COMMIT;\n");
        o("BEGIN TRANSACTION;\n");
      };
      (_, files) = kv;
      for (file in files) {
        row = RowValues::fromFile(file);
        for (_ in Range(0, row.repeat)) {
          o("INSERT INTO " + dirDescr.name + " VALUES ");
          o(row.toString());
          o(";\n");
          !count = count + 1;
        };
      };
    };
    o("COMMIT;\n");
    flushStdout();
    dirDescr
  });
}

fun dumpView(origContext: readonly SKFS.Context, viewName: String): void {
  context = SKFS.Context::fromSaved(origContext.clone());
  hviews = getViewsDir(context);
  viewsDir = context.unsafeGetEagerDir(hviews.dirName);
  arr = viewsDir.getArrayRaw(SKFS.SID(viewName));
  if (arr.size() == 0) {
    print_error("View " + viewName + " not found");
    skipExit(104);
  };
  invariant(arr.size() == 1);
  print_raw(SelectFile::fromFile(arr[0]).text);
  print_raw(";\n");
  flushStdout();
}

fun dumpViews(origContext: readonly SKFS.Context): void {
  context = SKFS.Context::fromSaved(origContext.clone());
  handle = getViewsDir(context);
  sinkName = SKFS.DirName::create("/sink_" + SKFS.genSym(0) + "/");
  sortedName = SKFS.DirName::create("/sorted_" + SKFS.genSym(0) + "/");
  sorted = handle.contextWriterKeyValues(
    handle.conv,
    context,
    sortedName,
    (_context, writer, _key, values) ~> {
      for (selectFile in values) {
        writer.write(SKFS.IID(selectFile.tick), selectFile)
      }
    },
  );
  _ = sorted.contextWriterKeyIter(
    sorted.conv,
    context,
    sinkName,
    (_context, _writer, _key, views) ~> {
      o = print_raw;
      for (view in views) {
        o(view.text);
        o(";\n");
      };
      flushStdout();
    },
  );
}

/*****************************************************************************/
/* File used for globals. */
/*****************************************************************************/

const GLOBALS: String = "GLOBALS";

class Globals(map: SortedMap<String, Int> = SortedMap[]) extends SKFS.File {
  fun set(var: String, val: Int): this {
    !this.map[var] = val;
    this
  }
  fun containsKey(var: String): Bool {
    this.map.containsKey(var)
  }
  fun get(var: String): Int {
    this.map[var]
  }
  fun maybeGet(var: String): ?Int {
    this.map.maybeGet(var)
  }
}
