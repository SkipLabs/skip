/*****************************************************************************/
/* The compiled abstract syntax tree. */
/*****************************************************************************/
module alias P = SQLParser;

module SKDB;

/*****************************************************************************/
/* Opaque types */
/*****************************************************************************/

const minSchema: CType = CType(SKJSON.Type::any);
const maxSchema: CType = CType(
  SKJSON.Type[
    (
      Int::max,
      SKJSON.TObject(
        SKJSON.Fields::create(
          Array[
            SKJSON.Field(
              largestString(),
              SKJSON.FieldCount::create(),
              SKJSON.Type::any,
            ),
          ],
        ),
      ),
    ),
  ],
);

class CType(ty: SKJSON.Type) extends CValue {
  static fun create(ty: SKJSON.Type): this {
    static(ty)
  }

  fun min(): CValue {
    minSchema
  }

  fun max(): CValue {
    maxSchema
  }
}

const minJSON: CJSON = CJSON(SKJSON.CJNull());

const maxJSON: CJSON = CJSON(
  SKJSON.CJObject(
    SKJSON.CJFields::create(Array[(largestString(), SKJSON.CJNull())], x -> x),
  ),
);

class CJSON(value: SKJSON.CJSON) extends SKDB.CValue {
  fun max(): SKDB.CValue {
    maxJSON
  }

  fun min(): SKDB.CValue {
    minJSON
  }
}

/*****************************************************************************/
/* Path interning for JSON. */
/*****************************************************************************/

class CStringFile(value: ?CString) extends SKStore.Key, SKStore.File

const internedCStringsDirName: SKStore.DirName = SKStore.DirName::create(
  "/json/interned_paths/",
);
const internedCStringsDir: SKStore.EHandle<
  SKStore.SID,
  CStringFile,
> = SKStore.EHandle(
  SKStore.SID::keyType,
  CStringFile::type,
  internedCStringsDirName,
);

fun initInternCStrings(context: mutable SKStore.Context): void {
  context.unsafeMaybeGetEagerDir(internedCStringsDirName) match {
  | None() ->
    _ = context.mkdir(
      SKStore.SID::keyType,
      CStringFile::type,
      internedCStringsDirName,
    )
  | Some(_) -> void
  }
}

fun internCString(context: mutable SKStore.Context, string: String): ?CString {
  initInternCStrings(context);
  key = SKStore.SID(string);
  cstring = Some(CString(string));
  for (file in internedCStringsDir.unsafeGetArray(context, key)) {
    return file.value;
  };
  internedCStringsDir.writeArray(context, key, Array[CStringFile(cstring)]);
  cstring
}

/*****************************************************************************/
/* FieldNames interning for JSON. */
/*****************************************************************************/

class FieldNamesFile(value: Array<String>) extends SKStore.File

const internedFieldsDirName: SKStore.DirName = SKStore.DirName::create(
  "/json/interned/",
);
const internedFieldsDir: SKStore.EHandle<
  SKStore.IID,
  FieldNamesFile,
> = SKStore.EHandle(
  SKStore.IID::keyType,
  FieldNamesFile::type,
  internedFieldsDirName,
);

fun initInternFieldNames(context: mutable SKStore.Context): void {
  context.unsafeMaybeGetEagerDir(internedFieldsDirName) match {
  | None() ->
    _ = context.mkdir(
      SKStore.IID::keyType,
      FieldNamesFile::type,
      internedFieldsDirName,
    )
  | Some(_) -> void
  }
}

fun internFieldNames(
  context: mutable SKStore.Context,
  fieldNames: Array<String>,
): Array<String> {
  key = SKStore.IID(fieldNames.hash());
  for (file in internedFieldsDir.unsafeGetArray(context, key)) {
    if (file.value == fieldNames) {
      return file.value;
    }
  };
  internedFieldsDir.writeArray(context, key, Array[FieldNamesFile(fieldNames)]);
  fieldNames
}

fun decodeJSON(context: mutable SKStore.Context, str: String): SKJSON.CJSON {
  initInternFieldNames(context);
  SKJSON.decode(str, x -> internFieldNames(context, x))
}

/*****************************************************************************/
/* Patterns */
/*****************************************************************************/

value class Pattern{
  firstIsPercent: Bool,
  lastIsPercent: Bool,
  subs: Array<Array<Char>>,
} uses Orderable, Hashable

base class AggrKind uses Orderable {
  children =
  | Noop()
  | Sum(Bool)
  | Count(Bool)
  | Min()
  | Max()
  | InferSchema()
}

extension base class CValue uses Show {
  fun getInt(): ?Int
  | CInt(n) -> Some(n)
  | _ -> None()

  fun +(y: CValue): CValue {
    (this, y) match {
    | (CInt(n1), CInt(n2)) -> CInt(n1 + n2)
    | (CFloat(f1), CFloat(f2)) -> CFloat(f1 + f2)
    | _ -> invariant_violation("Invalid type")
    }
  }

  fun -(y: CValue): CValue {
    (this, y) match {
    | (CInt(n1), CInt(n2)) -> CInt(n1 - n2)
    | (CFloat(f1), CFloat(f2)) -> CFloat(f1 - f2)
    | _ -> invariant_violation("Invalid type")
    }
  }

  fun *(y: Int): CValue {
    this match {
    | CInt(n) -> CInt(n * y)
    | CFloat(f) -> CFloat(f * y.toFloat())
    | _ -> invariant_violation("Invalid type")
    }
  }

  fun toString(): String {
    this match {
    | CInt(n) -> n.toString()
    | CFloat(f) -> inspect(f).toString()
    | CString(str) -> str
    | CType(ty) -> SKJSON.prettyPrintType(true, ty)
    | CJSON(json) -> json.prettyPrint()
    }
  }

  fun toStringWithFormat(format: SKStore.OutputFormat): String {
    this match {
    | CInt(n) -> n.toString()
    | CFloat(f) -> floatToString(f)
    | CString(str) if (format is SKStore.OCSV _) -> SKCSV.escapeString(str)
    | CString(str) if (format.usesDoubleQuotes()) ->
      result = mutable Vector['"'];
      for (c in str) {
        if (c == '"') {
          format match {
          | SKStore.OJSON _ -> result.push('\\')
          | SKStore.OSQL _ -> void
          | SKStore.OTable _ -> result.push(c)
          | SKStore.OCSV _ ->
            invariant_violation(
              "CSV handled separately. This branch should not be reached.",
            )
          | SKStore.OJS _ ->
            invariant_violation(
              "Internal error: JS direct mode should never be pretty-printed",
            )
          }
        };
        result.push(c);
      };
      result.push('"');
      String::fromChars(result.toArray())
    | CString(str) -> str
    | b @ CType _ -> CString(b.toString()).toStringWithFormat(format)
    | b @ CJSON _ -> CString(b.toString()).toStringWithFormat(format)
    }
  }

  fun max(): CValue
  | CInt(_) -> CInt(Int::max)
  | CFloat(_) -> CFloat(Float::inf)
  | CString(_) -> CString(largestString())

  fun min(): CValue
  | CInt(_) -> CInt(Int::min)
  | CFloat(_) -> CFloat(-Float::inf)
  | CString(_) -> CString("")
}

base class CGroupByElt uses Equality {
  children =
  | CGBExpr(CGExpr, P.Type)
  | CGBCol(Int, P.Type)
}

base class SubSelectTask uses Equality {
  children =
  | SubSelect(CSelect)
  | SubMerge{
    leftDirName: SKStore.DirName,
    leftColNbr: Int,
    leftIsUnique: Bool,
    leftRanges: ?Array<SKStore.KeyRange>,
    rightDirName: SKStore.DirName,
    rightColNbr: Int,
    rightIsUnique: Bool,
    rightRanges: ?Array<SKStore.KeyRange>,
    childName: SKStore.DirName,
    kinds: Array<(Int, P.IKind, P.Type)>,
    joinKind: P.JoinKind,
    onlyLeftValues: Bool = false,
  }
}

class CSelect{
  isNoop: Bool = false,
  id: P.Name,
  distinct: Bool,
  params: Array<CGExpr>,
  effects: Array<CGExpr>,
  sets: Array<(Int, CGExpr)>,
  aggr: ?Array<(AggrKind, CGExpr)>,
  kinds: Array<(Int, P.IKind, P.Type)>,
  from: Array<(DirDescr, ?Array<SKStore.KeyRange>)>,
  where: ?CExpr<Int>,
  groupBy: ?Array<CGroupByElt>,
  having: ?CExpr<Int>,
  orderBy: ?Array<(Int, P.IKind)>,
  limit: ?CExpr<Int>,
  rest: List<(QueryKind, CSelect)>,
  needUp: Bool,
  directMode: Bool,
  format: SKStore.OutputFormat,
  other: Array<SubSelectTask>,
} uses Equality

class CJoin{
  kind: P.JoinKind,
  names: Array<P.SelectSource>,
  on: ?Array<(CGExpr, ?String)>,
}

base class CGExpr uses Equality, Hashable {
  children =
  | CGNull()
  | CIExpr(CExpr<Int>)
  | CSExpr(CExpr<String>)
  | CFExpr(CExpr<Float>)
  | CJExpr(CExpr<CJSON>)
  | CTExpr(CExpr<CType>)

  fun up(): CGExpr {
    this match {
    | CGNull() -> CGNull()
    | CIExpr(ce) -> CIExpr(CUp(ce))
    | CSExpr(ce) -> CSExpr(CUp(ce))
    | CFExpr(ce) -> CFExpr(CUp(ce))
    | CJExpr(ce) -> CJExpr(CUp(ce))
    | CTExpr(ce) -> CTExpr(CUp(ce))
    }
  }

  fun getType(): P.Type {
    this match {
    | CGNull _
    | CIExpr _ ->
      P.INTEGER()
    | CFExpr _ -> P.FLOAT()
    | CSExpr _ -> P.TEXT()
    | CJExpr _ -> P.JSON()
    | CTExpr _ -> P.SCHEMA()
    }
  }
}

@cpp_extern("SKIP_largest_string")
native fun largestString(): String;

base class CExpr<Value: frozen> uses Equality, Hashable
class CNull<T: frozen>() extends CExpr<T>
class CISize(Int) extends CExpr<Int>

class CFICast(CExpr<Int>) extends CExpr<Float>
class CIFCast(CExpr<Float>) extends CExpr<Int>
class CISCast(CExpr<String>) extends CExpr<Int>
class CSICast(CExpr<Int>) extends CExpr<String>
class CSFCast(CExpr<Float>) extends CExpr<String>
class CFSCast(CExpr<String>) extends CExpr<Float>
class CJSCast(CExpr<CJSON>) extends CExpr<String>
class CTSCast(CExpr<CType>) extends CExpr<String>

class CIGetFrom(Int, Int) extends CExpr<Int>
class CSGetFrom(Int, Int) extends CExpr<String>
class CFGetFrom(Int, Int) extends CExpr<Float>
class CJGetFrom(Int, Int) extends CExpr<CJSON>
class CTGetFrom(Int, Int) extends CExpr<CType>

class CILiteral(Int) extends CExpr<Int>
class CFLiteral(Float) extends CExpr<Float>
class CSLiteral(String) extends CExpr<String>
class CNot(CExpr<Int>) extends CExpr<Int>

class CILte(CExpr<Int>, CExpr<Int>) extends CExpr<Int>
class CILt(CExpr<Int>, CExpr<Int>) extends CExpr<Int>
class CIGte(CExpr<Int>, CExpr<Int>) extends CExpr<Int>
class CIGt(CExpr<Int>, CExpr<Int>) extends CExpr<Int>
class CIEq(CExpr<Int>, CExpr<Int>) extends CExpr<Int>
class CIDiff(CExpr<Int>, CExpr<Int>) extends CExpr<Int>
class CIBetween(CExpr<Int>, CExpr<Int>, CExpr<Int>) extends CExpr<Int>
class CINotBetween(CExpr<Int>, CExpr<Int>, CExpr<Int>) extends CExpr<Int>

class CFLte(CExpr<Float>, CExpr<Float>) extends CExpr<Int>
class CFLt(CExpr<Float>, CExpr<Float>) extends CExpr<Int>
class CFGte(CExpr<Float>, CExpr<Float>) extends CExpr<Int>
class CFGt(CExpr<Float>, CExpr<Float>) extends CExpr<Int>
class CFEq(CExpr<Float>, CExpr<Float>) extends CExpr<Int>
class CFDiff(CExpr<Float>, CExpr<Float>) extends CExpr<Int>
class CFBetween(CExpr<Float>, CExpr<Float>, CExpr<Float>) extends CExpr<Int>
class CFNotBetween(CExpr<Float>, CExpr<Float>, CExpr<Float>) extends CExpr<Int>

class CSLte(CExpr<String>, CExpr<String>) extends CExpr<Int>
class CSLt(CExpr<String>, CExpr<String>) extends CExpr<Int>
class CSGte(CExpr<String>, CExpr<String>) extends CExpr<Int>
class CSGt(CExpr<String>, CExpr<String>) extends CExpr<Int>
class CSEq(CExpr<String>, CExpr<String>) extends CExpr<Int>
class CSDiff(CExpr<String>, CExpr<String>) extends CExpr<Int>
class CSBetween(CExpr<String>, CExpr<String>, CExpr<String>) extends CExpr<Int>
class CSNotBetween(
  CExpr<String>,
  CExpr<String>,
  CExpr<String>,
) extends CExpr<Int>
class CSLike(CExpr<String>, Pattern) extends CExpr<Int>

class CAdd<T: frozen>(CExpr<T>, CExpr<T>) extends CExpr<T>
class CSub<T: frozen>(CExpr<T>, CExpr<T>) extends CExpr<T>
class CMul<T: frozen>(CExpr<T>, CExpr<T>) extends CExpr<T>
class CDiv<T: frozen>(CExpr<T>, CExpr<T>) extends CExpr<T>
class CMod<T: frozen>(CExpr<T>, CExpr<T>) extends CExpr<T>

class CIf<T: frozen>(CExpr<Int>, CExpr<T>, CExpr<T>) extends CExpr<T>

class CIIn(CExpr<Int>, Array<CExpr<Int>>) extends CExpr<Int>
class CFIn(CExpr<Float>, Array<CExpr<Float>>) extends CExpr<Int>
class CSIn(CExpr<String>, Array<CExpr<String>>) extends CExpr<Int>

class CExists(cselect: CSelect) extends CExpr<Int> {
  fun hash(): Int {
    this.cselect.id.hash()
  }
}
class CIQuery(cselect: CSelect) extends CExpr<Int> {
  fun hash(): Int {
    this.cselect.id.hash()
  }
}
class CFQuery(cselect: CSelect) extends CExpr<Float> {
  fun hash(): Int {
    this.cselect.id.hash()
  }
}
class CSQuery(cselect: CSelect) extends CExpr<String> {
  fun hash(): Int {
    this.cselect.id.hash()
  }
}
class CJQuery(cselect: CSelect) extends CExpr<CJSON> {
  fun hash(): Int {
    this.cselect.id.hash()
  }
}
class CTQuery(cselect: CSelect) extends CExpr<CType> {
  fun hash(): Int {
    this.cselect.id.hash()
  }
}

class CIAbs(CExpr<Int>) extends CExpr<Int>
class CFAbs(CExpr<Float>) extends CExpr<Float>
class COr(CExpr<Int>, CExpr<Int>) extends CExpr<Int>
class CAnd(CExpr<Int>, CExpr<Int>) extends CExpr<Int>

class CUp<T: frozen>(CExpr<T>) extends CExpr<T>
class CCoalesce<T: frozen>(Array<CExpr<T>>) extends CExpr<T>
class CIIsNull(CExpr<Int>) extends CExpr<Int>
class CFIsNull(CExpr<Float>) extends CExpr<Int>
class CSIsNull(CExpr<String>) extends CExpr<Int>
class CJIsNull(CExpr<CJSON>) extends CExpr<Int>
class CTIsNull(CExpr<CType>) extends CExpr<Int>
class CSConcat(CExpr<String>, CExpr<String>) extends CExpr<String>
class CSUpper(CExpr<String>) extends CExpr<String>
class CSLower(CExpr<String>) extends CExpr<String>
class CSLength(CExpr<String>) extends CExpr<Int>

class CNullIf<T: frozen>(CExpr<T>, CExpr<T>) extends CExpr<T>
class CStrftime(
  CExpr<String>,
  CExpr<String>,
  Array<CExpr<String>>,
) extends CExpr<String>

class COn(P.JoinKind, CExpr<Int>) extends CExpr<Int>

class CISeqNum(Bool) extends CExpr<Int>
class CSId() extends CExpr<String>

class CICheck(Int, String, CExpr<Int>, ?P.Name) extends CExpr<Int>

/*****************************************************************************/
/* JSON primitives */
/*****************************************************************************/

class CJJSON(CExpr<String>) extends CExpr<CJSON>
class CTJSchema(CExpr<String>) extends CExpr<CType>
class CSJSchemaPp(CExpr<CType>) extends CExpr<String>
class CSJSchemaCheck(CExpr<CType>, CExpr<CType>) extends CExpr<String>
class CTJSONType(CExpr<CJSON>) extends CExpr<CType>
class CTParseSchema(CExpr<String>) extends CExpr<CType>

module end;
