module alias T = SKTest;

module SKFSTest;

@test
fun testSize(): void {
  context = SKFS.run(context ~> {
    dirSInput = context.mkdir(
      SKFS.StringFile::fromFile,
      SKFS.DirName::create("/sinput/"),
      false,
      Array[
        (SKFS.IID(0), SKFS.StringFile("23")),
        (SKFS.IID(1), SKFS.StringFile("35")),
      ],
    );

    _dir = dirSInput.contextMap(
      SKFS.IntFile::fromFile,
      context,
      SKFS.DirName::create("/input/"),
      (context, _) ~> SKFS.IntFile(dirSInput.size(context)),
    );
  });

  /***************************************************************************/
  // STARTING TESTS

  dirSInput = SKFS.DirName::create("/sinput/");
  dir = SKFS.DirName::create("/input/");

  T.expectEq(
    Array<SKFS.File>[SKFS.IntFile(2)],
    getData(context, dir, SKFS.IID(0)),
    "Test size init",
  );

  write(context, dirSInput, SKFS.IID(3), Array[SKFS.StringFile("3")]);
  context.update();

  T.expectEq(
    Array<SKFS.File>[SKFS.IntFile(3)],
    getData(context, dir, SKFS.IID(0)),
    "Test size after new file",
  );

  write(context, dirSInput, SKFS.IID(3), Array[SKFS.StringFile("4")]);
  context.update();

  T.expectEq(
    Array<SKFS.File>[SKFS.IntFile(3)],
    getData(context, dir, SKFS.IID(0)),
    "Test size after file modif",
  );

  write(context, dirSInput, SKFS.IID(3), Array[]);
  context.update();

  T.expectEq(
    Array<SKFS.File>[SKFS.IntFile(2)],
    getData(context, dir, SKFS.IID(0)),
    "Test size after file removal",
  )
}
