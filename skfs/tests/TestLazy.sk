module SKFSTest;

fun testLazy(): mutable SKFS.Context {
  context = SKFS.run(context ~> {
    dirInput = context.mkdir(
      SKFS.IntFile::fromFile,
      SKFS.DirName::create("/input/"),
      false,
      Array[(SKFS.IID(0), SKFS.IntFile(23))],
    );

    dirInput2 = context.mkdir(
      SKFS.IntFile::fromFile,
      SKFS.DirName::create("/input2/"),
      false,
      Array[(SKFS.IID(0), SKFS.IntFile(44))],
    );
    dir1 = SKFS.LHandle::create(
      SKFS.IntFile::fromFile,
      context,
      SKFS.DirName::create("/lazy1/"),
      (context, _self, x) ~> {
        Array[
          SKFS.IntFile(
            x match {
            | SKFS.IID(v) ->
              dirInput.getArray(context, SKFS.IID(0))[0].value + v
            | _ -> error()
            },
          ),
        ]
      },
    );
    dir2 = SKFS.LHandle::create(
      SKFS.IntFile::fromFile,
      context,
      SKFS.DirName::create("/lazy2/"),
      (context, _self, key) ~> {
        Array[SKFS.IntFile(dir1.getArray(context, key)[0].value)]
      },
    );

    adir1 = SKFS.LHandle::createAsync(
      SKFS.IntFile::fromFile,
      context,
      SKFS.DirName::create("/alazy1/"),
      (context, _self, x) ~> {
        Some(
          Array[
            SKFS.IntFile(
              x match {
              | SKFS.IID(v) ->
                dirInput.getArray(context, SKFS.IID(0))[0].value + v
              | _ -> error()
              },
            ),
          ],
        )
      },
    );
    _adir2 = SKFS.LHandle::createAsync(
      SKFS.IntFile::fromFile,
      context,
      SKFS.DirName::create("/alazy2/"),
      (context, _self, key) ~> {
        arr = adir1.getArrayAsync(context, key);
        if (arr.size() == 0) None() else Some(Array[SKFS.IntFile(arr[0].value)])
      },
    );

    _dir3 = dirInput2.contextWriterKeyMap(
      SKFS.IntFile::fromFile,
      context,
      SKFS.DirName::create("/eager/"),
      (context, writer, key, _) ~> {
        writer.write(key, dir2.getArray(context, key)[0]);
      },
    )
  });

  input = SKFS.DirName::create("/input/");
  dir2 = SKFS.DirName::create("/lazy2/");
  adir2 = SKFS.DirName::create("/alazy2/");
  dir3 = SKFS.DirName::create("/eager/");

  expectEq("Async Lazy basic", Array<SKFS.File>[], () ->
    getLazyData(context, adir2, SKFS.IID(0), true)
  );

  _ = context.updateWithStatus(0);

  expectEq("Async Lazy basic with interruption", Array<SKFS.File>[], () ->
    getLazyData(context, adir2, SKFS.IID(0), true)
  );

  _ = context.updateWithStatus(30);

  expectEq(
    "Async Lazy basic without interruption",
    Array<SKFS.File>[SKFS.IntFile(23)],
    () -> getLazyData(context, adir2, SKFS.IID(0), true),
  );

  expectEq("Lazy basic", Array<SKFS.File>[SKFS.IntFile(23)], () ->
    getData(context, dir2, SKFS.IID(0))
  );
  write(context, input, SKFS.IID(0), Array[SKFS.IntFile(22)]);
  _status = context.updateWithStatus(10);

  expectEq("Lazy after update", Array<SKFS.File>[SKFS.IntFile(22)], () ->
    getData(context, dir2, SKFS.IID(0))
  );

  expectEq("Eeager after update", Array<SKFS.File>[SKFS.IntFile(22)], () ->
    getData(context, dir3, SKFS.IID(0))
  );

  context
}
