/*****************************************************************************/
/* Parsing the entries (used the option --write). */
/*****************************************************************************/

module SKFS;

/*****************************************************************************/
/* Escaping functions. */
/*****************************************************************************/

fun escape(str: String): String {
  output = mutable Vector[];
  for (c in str) {
    if (c == '\\') {
      output.push('\\');
      output.push('\\');
    } else if (c == '\t') {
      output.push('\\');
      output.push('t');
    } else if (c == '\n') {
      output.push('\\');
      output.push('n');
    } else if (c == '"') {
      output.push('\\');
      output.push('"');
    } else {
      output.push(c);
    }
  };
  String::fromChars(output.toArray())
}

fun unescape(input: readonly Vector<Char>): String {
  output = mutable Vector[];
  i = 0;
  while (i < input.size()) {
    input[i] match {
    | '\\' ->
      if (i + 1 >= input.size()) {
        output.push('\\');
        break void;
      };
      input[i + 1] match {
      | 't' ->
        output.push('\t');
        !i = i + 1
      | 'n' ->
        output.push('\n');
        !i = i + 1
      | '"' ->
        output.push('"');
        !i = i + 1
      | '\\' ->
        output.push('\\');
        !i = i + 1
      | _ -> output.push('\\')
      }
    | c -> output.push(c)
    };
    !i = i + 1;
  };
  String::fromChars(output.toArray())
}

fun getWriteKey(readAt: () -> Char): (?String, Bool) {
  chars = mutable Vector[];
  vtry<(?String, Bool)>(
    () -> {
      loop {
        c = readAt();
        if (c == '\\') {
          chars.push(c);
          continue
        };
        if (c == '\n') {
          return (None(), false);
        };
        if (c == '\t') {
          return (Some(unescape(chars)), false)
        };
        chars.push(c);
      }
    },
    _ -> return (None(), true),
  )
}

fun getWriteValue(readAt: () -> Char): ?String {
  chars = mutable Vector[];
  vtry<?String>(
    () -> {
      first = readAt();
      if (first != '"') return None();

      loop {
        c = readAt();
        if (c == '\\') {
          chars.push(c);
          continue
        };
        if (c == '\n') {
          return Some(unescape(chars.slice(0, chars.size() - 1)))
        };
        chars.push(c);
      }
    },
    _ -> return None(),
  )
}

fun getWriteKeyValueIter(
  readAt: () -> Char,
): mutable Iterator<?(String, ?String)> {
  loop {
    (key, isFinished) = getWriteKey(readAt);
    key match {
    | Some(k) -> yield (Some((k, getWriteValue(readAt))))
    | None() if (!isFinished) -> yield None()
    | None() -> void
    };
    if (isFinished) {
      break void;
    }
  }
}

base class KVStreamOp {
  children =
  | KVSep()
  | KVAcc(keyName: String, valueName: ?String)
}

fun getWriteStreamOpIter(readAt: () -> Char): mutable Iterator<?KVStreamOp> {
  lastKeyOpt: ?String = None();

  for (currentOpt in getWriteKeyValueIter(readAt)) {
    currentOpt match {
    | None() ->
      !lastKeyOpt = None();
      yield None()
    | Some(current) ->
      if (current.i0 == "") {
        yield Some(KVSep());
      } else {
        lastKeyOpt match {
        | None() -> yield Some(KVAcc(current.i0, current.i1))
        | Some(lastKey) ->
          if (current.i0 != lastKey) {
            yield Some(KVSep())
          };
          yield Some(KVAcc(current.i0, current.i1))
        }
      };
      !lastKeyOpt = Some(current.i0)
    }
  };

  yield Some(KVSep())
}

fun getWriteKeyValuesIter(
  readAt: () -> Char,
): mutable Iterator<(BaseName, Array<File>)> {
  keyOpt: ?String = Some("");
  values = mutable Vector[];
  for (opOpt in getWriteStreamOpIter(readAt)) {
    opOpt match {
    | None() ->
      values.clear();
      !keyOpt = None()
    | Some(op) ->
      op match {
      | KVSep() ->
        keyOpt match {
        | None() -> void
        | Some(key) -> yield (SID::create(key), values.toArray())
        };
        values.clear()
      | KVAcc(k, value) ->
        !keyOpt = Some(k);
        value match {
        | None() -> values.clear()
        | Some(v) -> values.push(SKFS.StringFile(v))
        }
      }
    }
  }
}

fun writeFromStdin(context: mutable Context, dirNameStr: String): void {
  dirName = SKFS.DirName::create(dirNameStr);
  dir = context.unsafeGetEagerDir(dirName);
  i = 0;
  next = () -> {
    !i = i + 1;
    getChar(i);
  };
  dir.writeArrayMany(context, getWriteKeyValuesIter(next));
}

module end;
