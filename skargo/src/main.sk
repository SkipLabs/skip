module Skargo;

fun getProfile(args: Cli.ParseResults): String {
  args.maybeGetString("profile") match {
  | Some(p) -> p
  | None() if (args.getBool("release")) -> kReleaseProfile
  | None() -> kDevProfile
  }
}

fun execInit(_args: Cli.ParseResults, _env: Env): void {
  invariant_violation("TODO");
  void
}

fun execUpdate(_args: Cli.ParseResults, _env: Env): void {
  invariant_violation("TODO");
  void
}

fun execTest(args: Cli.ParseResults, env: Env): void {
  bc = BuildContext::create(
    env,
    args.maybeGetString("target"),
    args.maybeGetString("target-dir"),
    getProfile(args),
  );
  genVersionFile(bc);
  compile(bc, "test", env.manifest.testHarness);
  filter = args.maybeGetString("filter");
  junitxml = args.maybeGetString("junitxml").map(f -> `--junitxml=${f}`);
  rv = system(
    Array[Path.join(bc.targetDir, "test")].concat(
      Array[junitxml, filter].filterNone(),
    ).join(" "),
  );
  // TODO: Properly handle subprocess not exiting normally.
  if (rv != 0) {
    skipExit(1)
  }
}

fun execBuild(args: Cli.ParseResults, env: Env): void {
  bc = BuildContext::create(
    env,
    args.maybeGetString("target"),
    args.maybeGetString("target-dir"),
    getProfile(args),
  );
  genVersionFile(bc);
  for (bin in env.manifest.bin) {
    compile(bc, bin.name, bin.main)
  }
}

fun execClean(args: Cli.ParseResults, env: Env): void {
  bc = BuildContext::create(
    env,
    /* targetOpt = */ None(),
    args.maybeGetString("target-dir"),
    getProfile(args),
  );
  run(Array["rm", "-rf", bc.targetDir], env.verbose)
}

fun execFmt(_args: Cli.ParseResults, env: Env): void {
  files = env.manifest.srcs.concat(env.manifest.tests);
  run(Array["skfmt", "-i"].concat(files), env.verbose)
}

fun execCheck(args: Cli.ParseResults, env: Env): void {
  bc = BuildContext::create(
    env,
    args.maybeGetString("target"),
    args.maybeGetString("target-dir"),
    getProfile(args),
  );
  genVersionFile(bc);
  invokeSkc(bc, Array["--check"])
}

fun execHelp(_args: Cli.ParseResults, _env: Env): void {
  invariant_violation("TODO: Spawn manpage.")
}

fun main(): void {
  args = Cli.Command("skargo")
    .about("Skip's package manager")
    .arg(
      Cli.BoolArg("version")
        .short("V")
        .long("version")
        .about("Print version info and exit"),
    )
    .arg(
      Cli.BoolArg("verbose")
        .short("v")
        .long("verbose")
        .about("Use verbose output")
        .global(),
    )
    .help()
    .subcommand(
      Cli.Command("build")
        .short("b")
        .about("Compile a local package")
        .arg(Cli.StringArg("target").about("Build for the target triple"))
        .arg(
          Cli.StringArg("target-dir").about(
            "Directory for all generated artifacts",
          ),
        )
        .arg(
          Cli.StringArg("profile").about(
            "Build artifacts with the specified profile",
          ),
        )
        .arg(
          Cli.BoolArg("release")
            .short("r")
            .long("release")
            .about("Build artifacts in release mode, with optimizations"),
        ),
    )
    .subcommand(
      Cli.Command("check")
        .short("c")
        .about(
          "Analyze the current package and report errors, but don't build object files",
        )
        .arg(Cli.StringArg("target").about("Check for the target triple"))
        .arg(
          Cli.StringArg("target-dir").about(
            "Directory for all generated artifacts",
          ),
        )
        .arg(
          Cli.StringArg("profile").about(
            "Build artifacts with the specified profile",
          ),
        )
        .arg(
          Cli.BoolArg("release")
            .short("r")
            .long("release")
            .about("Build artifacts in release mode, with optimizations"),
        ),
    )
    .subcommand(
      Cli.Command("test")
        .short("t")
        .about("Run the tests")
        .arg(
          Cli.StringArg("filter")
            .positional()
            .about(
              "If specified, only run tests with names matching the filter",
            ),
        )
        .arg(Cli.StringArg("target").about("Check for the target triple"))
        .arg(
          Cli.StringArg("target-dir").about(
            "Directory for all generated artifacts",
          ),
        )
        .arg(
          Cli.StringArg("profile").about(
            "Build artifacts with the specified profile",
          ),
        )
        .arg(
          Cli.BoolArg("release")
            .short("r")
            .long("release")
            .about("Build artifacts in release mode, with optimizations"),
        )
        .arg(Cli.StringArg("junitxml").about("Generate a JUnit XML report")),
    )
    .subcommand(
      Cli.Command("clean")
        .about("Remove the target directory")
        .arg(
          Cli.StringArg("target-dir").about(
            "Directory for all generated artifacts",
          ),
        )
        .arg(
          Cli.StringArg("profile").about(
            "Build artifacts with the specified profile",
          ),
        )
        .arg(
          Cli.BoolArg("release")
            .short("r")
            .long("release")
            .about("Build artifacts in release mode, with optimizations"),
        ),
    )
    .subcommand(Cli.Command("fmt").about("Format all files using skfmt"))
    .subcommand(
      Cli.Command("init")
        .about("Create a new skargo package")
        .arg(Cli.StringArg("path").default(".")),
    )
    .subcommand(
      Cli.Command("update").about("Update dependencies listed in Skargo.lock"),
    )
    .subcommand(
      Cli.Command("help").about("Displays help for a skargo subcommand"),
    )
    .parseArgs();

  if (args.getBool("version")) {
    print_string("skargo " + SkargoVersion.kVersion);
    skipExit(0);
  } else if (args.maybeGetSubcommand().isNone()) {
    print_string(Cli.usage(args.cmd, true));
    skipExit(0)
  } else {
    subcmd_handler = args.maybeGetSubcommand().map(subcmd ->
      subcmd match {
      | "init" -> Skargo.execInit
      | "update" -> Skargo.execUpdate
      | "check" -> Skargo.execCheck
      | "build" -> Skargo.execBuild
      | "test" -> Skargo.execTest
      | "clean" -> Skargo.execClean
      | "fmt" -> Skargo.execFmt
      | "help" -> Skargo.execHelp
      | _ -> invariant_violation(`Unknown subcommand ${subcmd}`)
      }
    );

    env = Env::create(args.getBool("verbose"));
    subcmd_handler.each(f -> f(args, env))
  };

  void
}
