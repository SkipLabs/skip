module Skargo;

const kVersion: String = "0.1.0";
const kManifestFile: String = "Skargo.toml";

class Env{skipPath: String, rootDir: String, manifest: Manifest} {
  static fun create(): Env {
    skipPath = Path.parentname(Path.dirname(getMainExecutable(getArg0())));
    rootDir = findRootDir();
    Env{
      skipPath,
      rootDir,
      manifest => Manifest::read(Path.join(rootDir, kManifestFile)),
    }
  }
}

// Return the path to the main executable, given the value of argv[0] from
// program startup.
// Inspired from `llvm/lib/Support/Unix/Path.inc`.
private fun getMainExecutable(argv0: String): String {
  // First approach: absolute path.
  if (argv0.startsWith("/")) {
    path = argv0;
    if (FileSystem.exists(path)) {
      return FileSystem.realpath(path)
    }
  };

  // Second approach: relative path.
  if (argv0.contains("/")) {
    path = Path.join(getcwd(), argv0);
    if (FileSystem.exists(path)) {
      return FileSystem.realpath(path)
    }
  };

  // Third approach: $PATH
  for (p in getenv("PATH").split(":")) {
    path = Path.join(p, argv0);
    if (FileSystem.exists(path)) {
      return FileSystem.realpath(path)
    }
  };

  invariant_violation("Cannot determine main executable.")
}

private fun findRootDir(): String {
  path = getcwd();

  loop {
    if (FileSystem.exists(Path.join(path, kManifestFile))) {
      return path
    };
    if (Path.isRoot(path)) {
      invariant_violation(
        `Could not find \`${kManifestFile}\` in \`${getcwd()} or any parent directory.\``,
      )
    };

    !path = Path.parentname(path)
  }
}

private fun run(cmd: String, verbose: Bool): void {
  if (verbose) {
    // TODO: colors
    print_error(`>> ${cmd}`)
  };
  rv = system(cmd);
  if (rv != 0) {
    invariant_violation("error")
  };
}

private fun compile(
  files: readonly Sequence<String>,
  binName: String,
  main: String,
  targetDir: String,
  target: ?String,
  env: Env,
  verbose: Bool,
): void {
  _ = system(`mkdir -p ${targetDir}`);

  // TODO: Invoke `skc` by default, and allow override through `SKC` env var.
  skc = Path.join(env.skipPath, "bin", "skc");
  output = Path.join(targetDir, binName);
  targetFlag = target.map(t -> " --target=" + t).default("");
  run(
    `${skc}${targetFlag} ${files.join(
      " ",
    )} --export-function-as ${main}=skip_main --emit=link --output ${output}`,
    verbose,
  )
}

private fun build(
  manifest: Manifest,
  name: String,
  main: String,
  targetDir: String,
  target: ?String,
  env: Env,
  verbose: Bool,
): void {
  files = manifest.srcs.concat(manifest.tests);
  deps = getDependencies(manifest) match {
  | Success(v) -> v
  | Failure(_) -> invariant_violation("Unsatisfiable dep")
  };

  for (dep in deps) {
    !files = files.concat(dep.srcs)
  };
  compile(files, name, main, targetDir, target, env, verbose);
  void
}
