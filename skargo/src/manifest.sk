module Skargo;

class Version(major: Int, minor: Int, patch: Int) uses Orderable {
  static fun fromString(str: String): Version {
    components = str.split(".");
    if (components.size() != 3) {
      invariant_violation("Invalid version string " + str)
    };
    Version(components[0].toInt(), components[1].toInt(), components[2].toInt())
  }

  fun toString(): String {
    `${this.major}.${this.minor}.${this.patch}`
  }
}

base class VersionConstraint {
  children =
  | RangeConstraint(low: Version, high: ?Version)
  | PathConstraint(path: String, version: ?Version)

  static fun fromTOML(basePath: String, toml: TOML.Value): VersionConstraint {
    toml match {
    | TOML.String(v) -> static::fromString(v)
    | t @ TOML.Table _ ->
      t.maybeGetString("path") match {
      | Some(path) ->
        PathConstraint(
          Path.join(basePath, path),
          t.maybeGetString("version").map(Version::fromString),
        )
      | _ -> invariant_violation("TODO")
      }
    | _ -> invariant_violation("Invalid dependency specification.")
    };
  }

  static fun fromString(str: String): VersionConstraint {
    lexer = VersionLexer::create(str);
    lexer.lex();
  }
}

class TargetTriplet(
  machine: String,
  vendor: ?String,
  operatingsystem: ?String,
) uses Orderable, Show {
  //
  static fun fromString(str: String): TargetTriplet {
    components = str.split("-");
    if (components.isEmpty() || components.size() > 3) {
      invariant_violation("Invalid target triplet string " + str)
    };
    TargetTriplet(
      components[0],
      components.maybeGet(1) match {
      | Some(v) if (v != "unknown") -> Some(v)
      | _ -> None()
      },
      components.maybeGet(2) match {
      | Some(v) if (v != "unknown") -> Some(v)
      | _ -> None()
      },
    )
  }

  fun isWasm32(): Bool {
    this.machine == "wasm32"
  }

  fun allows(target: TargetTriplet): Bool {
    this.machine == target.machine &&
      this.vendor.default("unknown") == target.vendor.default("unknown") &&
      this.operatingsystem.default("unknown") ==
        target.operatingsystem.default("unknown")
  }

  fun toString(): String {
    `${this.machine}-${this.vendor.default(
      "unknown",
    )}-${this.operatingsystem.default("unknown")}`
  }
}

class ManifestBinary(name: String, main: String) {
  static fun fromTOML(toml: TOML.Table): ManifestBinary {
    name = toml.getString("name");
    main = toml.getString("main");

    ManifestBinary(name, main)
  }
}

class Manifest{
  name: String,
  path: String,
  version: Version,
  srcs: Array<String>,
  tests: Array<String>,
  testHarness: String,
  dependencies: Map<String, VersionConstraint>,
  buildDependencies: Map<String, VersionConstraint>,
  bin: Array<ManifestBinary>,
} {
  static fun read(path: String): Manifest {
    basePath = Path.dirname(path);
    toml = TOML.decode(FileSystem.readTextFile(path));
    package = toml.getTable("package");
    name = package.getString("name");
    version = Version::fromString(package.getString("version"));

    srcs = package.maybeGetArray("srcs") match {
    | Some(arr) -> arr.map(x -> x.expectString())
    | _ ->
      FileSystem.readFilesRecursive(Path.join(basePath, "src"), f ->
        f.endsWith(".sk")
      ).toArray()
    };

    testHarness = package.maybeGetString("test-harness").default("SKTest.main");

    tests = package.maybeGetArray("tests") match {
    | Some(arr) -> arr.map(x -> x.expectString())
    | _ ->
      testsPath = Path.join(basePath, "tests");
      if (FileSystem.exists(testsPath)) {
        FileSystem.readFilesRecursive(testsPath, f ->
          f.endsWith(".sk")
        ).toArray()
      } else {
        Array[]
      }
    };

    dependencies = toml.getTable("dependencies").map.map((_, v) ->
      VersionConstraint::fromTOML(basePath, v)
    );

    buildDependencies = toml.maybeGetTable("build-dependencies") match {
    | Some(bDependencies) ->
      bDependencies.map.map((_, v) -> VersionConstraint::fromTOML(basePath, v))
    | _ -> Map[]
    };

    bin = toml.maybeGetArray("bin") match {
    | Some(arr) -> arr.map(t -> ManifestBinary::fromTOML(t.expectTable()))
    | None() -> Array[]
    };

    Manifest{
      name,
      path => basePath,
      version,
      srcs,
      tests,
      testHarness,
      dependencies,
      buildDependencies,
      bin,
    }
  }
}
