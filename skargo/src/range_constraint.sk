module Skargo;

class LexVersion(
  major: Int,
  minor: ?Int = None(),
  patch: ?Int = None(),
) uses Equality {
  //
  fun toCaretRangeConstraint(): RangeConstraint {
    (this.minor, this.patch) match {
    | (Some(minor), Some(patch)) ->
      if (this.major > 0) {
        //1.2.3  :=  >=1.2.3, <2.0.0
        RangeConstraint(
          Version(this.major, minor, patch),
          Some(Version(this.major + 1, 0, 0)),
        )
      } else if (minor > 0) {
        // 0.2.3  :=  >=0.2.3, <0.3.0
        RangeConstraint(
          Version(this.major, minor, patch),
          Some(Version(this.major, minor + 1, 0)),
        )
      } else {
        // 0.0.3  :=  >=0.0.3, <0.0.4
        RangeConstraint(
          Version(this.major, minor, patch),
          Some(Version(this.major, 0, patch + 1)),
        )
      }
    | (Some(minor), None()) ->
      if (this.major > 0) {
        // 1.2    :=  >=1.2.0, <2.0.0
        RangeConstraint(
          Version(this.major, minor, 0),
          Some(Version(this.major + 1, 0, 0)),
        )
      } else {
        // 0.2    :=  >=0.2.0, <0.3.0
        // 0.0    :=  >=0.0.0, <0.1.0
        RangeConstraint(
          Version(this.major, minor, 0),
          Some(Version(this.major, minor + 1, 0)),
        )
      }
    | (None(), None()) ->
      // 1      :=  >=1.0.0, <2.0.0
      // 0      :=  >=0.0.0, <1.0.0
      RangeConstraint(
        Version(this.major, 0, 0),
        Some(Version(this.major + 1, 0, 0)),
      )
    | _ -> invariant_violation("Unsupported version")
    }
  }

  fun toTildeRangeConstraint(): RangeConstraint {
    (this.minor, this.patch) match {
    | (Some(minor), Some(patch)) ->
      //~1.2.3  := >=1.2.3, <1.3.0
      RangeConstraint(
        Version(this.major, minor, patch),
        Some(Version(this.major, minor + 1, 0)),
      )
    | (Some(minor), None()) ->
      //~1.2    := >=1.2.0, <1.3.0
      RangeConstraint(
        Version(this.major, minor, 0),
        Some(Version(this.major, minor + 1, 0)),
      )
    | (None(), None()) ->
      //~1      := >=1.0.0, <2.0.0
      RangeConstraint(
        Version(this.major, 0, 0),
        Some(Version(this.major + 1, 0, 0)),
      )
    | _ -> invariant_violation("Unsupported version")
    }
  }

  fun toEquals(): RangeConstraint {
    low = Version(this.major, this.minor.default(0), this.patch.default(0));
    high = Version(
      this.major,
      this.minor.default(0),
      this.patch.default(0) + 1,
    );
    RangeConstraint(low, Some(high))
  }

  fun toVersion(next: Bool = false): Version {
    Version(
      this.major,
      this.minor.default(0),
      this.patch.default(0) + if (next) 1 else 0,
    )
  }
}

mutable class VersionLexer private {position: mutable Lexer.LexingPosition} {
  //
  mutable fun lex(): RangeConstraint {
    this.skipChars(Chars.isWhitespace);
    this.position.peek() match {
    | '~' ->
      this.position.advance();
      this.lexVersion().toTildeRangeConstraint()
    | '*' -> RangeConstraint(Version(0, 0, 0), None())
    | '=' ->
      this.position.advance();
      this.skipChars(Chars.isWhitespace);
      this.lexVersion().toEquals()
    | '>' ->
      this.position.advance();
      lstrict = true;
      this.position.peek() match {
      | '=' ->
        this.position.advance();
        !lstrict = false
      | _ -> void
      };
      this.skipChars(Chars.isWhitespace);
      low = this.lexVersion().toVersion(lstrict);
      this.skipChars(Chars.isWhitespace);
      this.position.peek() match {
      | ',' ->
        hstrict = true;
        this.position.advance();
        this.skipChars(Chars.isWhitespace);
        this.position.peek() match {
        | '<' ->
          this.position.advance();
          this.position.peek() match {
          | '=' ->
            this.position.advance();
            !hstrict = false
          | _ -> void
          }
        | _ -> void
        };
        this.skipChars(Chars.isWhitespace);
        high = this.lexVersion().toVersion(!hstrict);
        RangeConstraint(low, Some(high))
      | c if (c == Chars.null) -> RangeConstraint(low, None())
      | _ -> this.version_violation()
      }
    | '<' ->
      this.position.advance();
      this.skipChars(Chars.isWhitespace);
      hstrict = true;
      this.position.peek() match {
      | '=' ->
        this.position.advance();
        this.skipChars(Chars.isWhitespace);
        !hstrict = false
      | _ -> void
      };
      high = this.lexVersion().toVersion(!hstrict);
      RangeConstraint(Version(0, 0, 0), Some(high))
    | '^' ->
      this.position.advance();
      this.lexVersion().toCaretRangeConstraint()
    | c if (Chars.isDigit(c)) ->
      major = this.lexInt();
      this.position.peek() match {
      | '.' ->
        this.position.advance();
        this.position.peek() match {
        | '*' ->
          // 1.*   := >=1.0.0, <2.0.0
          RangeConstraint(Version(major, 0, 0), Some(Version(major + 1, 0, 0)))
        | _ ->
          minor = this.lexInt();
          this.position.peek() match {
          | '.' ->
            this.position.advance();
            this.position.peek() match {
            | '*' ->
              // 1.2.* := >=1.2.0, <1.3.0
              RangeConstraint(
                Version(major, minor, 0),
                Some(Version(major, minor + 1, 0)),
              )
            | _ ->
              patch = this.lexInt();
              LexVersion(
                major,
                Some(minor),
                Some(patch),
              ).toCaretRangeConstraint()
            }
          | c1 if (c1 == Chars.null || Chars.isWhitespace(c1)) ->
            LexVersion(major, Some(minor)).toCaretRangeConstraint()
          | _ -> this.version_violation()
          }
        }
      | c1 if (c1 == Chars.null || Chars.isWhitespace(c1)) ->
        LexVersion(major).toCaretRangeConstraint()
      | _ -> this.version_violation()
      }
    | _ -> this.version_violation()
    };
  }

  mutable fun lexVersion(): LexVersion {
    major = this.lexInt();
    this.position.peek() match {
    | '.' ->
      this.position.advance();
      minor = this.lexInt();
      this.position.peek() match {
      | '.' ->
        this.position.advance();
        patch = this.lexInt();
        LexVersion(major, Some(minor), Some(patch))
      | c if (c == Chars.null || Chars.isWhitespace(c) || c == ',') ->
        LexVersion(major, Some(minor))
      | _ -> this.version_violation()
      }
    | c if (c == Chars.null || Chars.isWhitespace(c) || c == ',') ->
      LexVersion(major)
    | _ -> this.version_violation()
    }
  }

  mutable fun lexInt(): Int {
    c = this.position.peek();
    invariant(Chars.isDigit(c), "Expected digit");
    current = 0;
    while (Chars.isDigit(c)) {
      v = c.code() - 48;
      !current = current * 10 + v;
      this.position.advance();
      !c = this.position.peek();
    };
    current
  }

  // Creates a Lexer positioned at the start of a String.
  static fun create(source: String): mutable this {
    static::createFromPosition(Lexer.LexingPosition::create(source));
  }

  static fun createFromPosition(
    position: mutable Lexer.LexingPosition,
  ): mutable this {
    mutable static{position}
  }

  // Skip characters while predicate is true.
  protected mutable fun skipChars(predicate: Char ~> Bool): void {
    loop {
      if (!predicate(this.position.peek())) break void;
      this.position.advance();
    }
  }

  protected readonly fun version_violation<T>(): T {
    invariant_violation(
      "Unsupported version constraint " + this.position.source,
    )
  }
}
