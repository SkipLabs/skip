/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

module SkipPrinter;

fun prettyPrint(
  contents: String,
  filename: String,
): Result<String, SkipError.Error> {
  parsed = SkipParser.parseSource(contents, false);
  if (parsed.errors.size() > 0) {
    // Parse errors are expected. Exceptions are bugs.
    Failure(SkipError.errorFromSyntaxError(parsed.errors[0], filename))
  } else {
    Success(SkipPrinter.printTreeToString(parsed.tree, contents) + "\n")
  }
}

untracked fun main(): void {
  args = Cli.Command("skfmt")
    .about("Format skip code")
    .arg(
      Cli.ArrayArg("files")
        .positional()
        .default(Array["-"])
        .about("Skip file(s) to format"),
    )
    .arg(
      Cli.BoolArg("inplace")
        .short("i")
        .about("Inplace edit file(s), if specified"),
    )
    .help()
    .parseArgs();

  inplace = args.getBool("inplace");
  filenames = args.getArray("files");
  for (file in filenames) {
    contents = if (file == "-") {
      !file = "<stdin>";
      buf = mutable Vector[];
      IO.stdin().read_to_end(buf) match {
      | Success _ -> String::fromUtf8(buf.collect(Array))
      | Failure(err) ->
        print_error(err.getMessage());
        skipExit(1)
      }
    } else {
      FileSystem.readTextFile(file)
    };
    prettyPrint(contents, file) match {
    | Failure(error) ->
      print_error_ln(SkipError.errorsToString(Vector[error]));
      skipExit(1)
    | Success(formattedContents) ->
      if (inplace) {
        FileSystem.writeTextFile(file, formattedContents)
      } else {
        print_raw(formattedContents)
      }
    }
  }
}

module end;
