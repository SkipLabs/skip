module Sknpm;

const kAuthor: String = "Skiplabs";

class Asset(name: String, path: String) {
  //
  static fun fromJSON(jsonValue: JSON.Value): Asset {
    json = jsonValue.expectObject();
    static(json.getString("name"), json.getString("path"))
  }

  fun toJSON(): JSON.Value {
    JSON.Object(
      Map<String, JSON.Value>[
        "name" => JSON.String(this.name),
        "path" => JSON.String(this.path),
      ],
    );
  }
}

class Package(
  name: String,
  author: ?String,
  version: Version,
  optNodeVersion: ?String = None(),
  binaries: Array<String> = Array[],
  assets: Array<Asset> = Array[],
  sideEffects: Bool = false,
) {
  static fun check(root: String, console: Skargo.Console): Result<void, void> {
    file = Path.join(root, "npm.json");
    if (!FileSystem.exists(file)) {
      console.error("npm.json file must be defined.");
      Failure(void)
    } else {
      Success(void)
    }
  }

  static fun create(
    root: String,
    name: String,
    console: Skargo.Console,
    test: Bool = false,
  ): Result<Package, void> {
    file = Path.join(root, "npm.json");
    if (FileSystem.exists(file)) {
      try {
        contents = FileSystem.readTextFile(file);
        json = JSON.decode(contents).expectObject();
        Version::fromString(json.getString("version")) match {
        | Success(version) ->
          Success(
            Package(
              json.getString("name"),
              json.maybeGetString("author"),
              version,
              json.maybeGetString("node-version"),
              json
                .maybeGetArray("binaries")
                .map(jsa -> jsa.value.map(v -> v.expectString()).toArray())
                .default(Array[]),
              json
                .maybeGetArray("assets")
                .map(jsa -> jsa.value.map(Asset::fromJSON).toArray())
                .default(Array[]),
              json.maybeGetBool("sideEffects").default(false),
            ),
          )
        | Failure(msg) ->
          console.error(msg);
          Failure(void)
        }
      } catch {
      | ex ->
        console.error(ex.getMessage());
        Failure(void)
      }
    } else {
      if (!test) console.warn("The npm.json file is not defined");
      // FIXME
      Version::fromString("0.2.0-FIXME") match {
      | Success(version) ->
        Success(Package(if (test) "sk_tests" else name, Some(kAuthor), version))
      | Failure(msg) ->
        console.error(msg);
        Failure(void)
      }
    }
  }

  fun update(path: String, version: Version): void {
    file = Path.join(path, "npm.json");
    stream = mutable Vector[];
    (this with {version}).toJSON().writeToStream(stream.push, 2);
    FileSystem.writeTextFile(file, stream.join(""));
  }

  fun scriptName(binName: String): String {
    prefix = `${this.name}-`;
    if (binName.startsWith(prefix)) {
      binName.substring(binName.getIter().drop(prefix.length()));
    } else {
      binName
    }
  }

  fun toJSON(): JSON.Value {
    map = mutable Map<String, JSON.Value>[
      "name" => JSON.String(this.name),
      "version" => JSON.String(this.version.toString()),
    ];
    this.author.each(a -> map.set("author", JSON.String(a)));
    this.optNodeVersion.each(v -> map.set("node-version", JSON.String(v)));
    if (!this.binaries.isEmpty()) {
      map.set(
        "binaries",
        JSON.Array(
          Vector::createFromItems(this.binaries.map(v -> JSON.String(v))),
        ),
      )
    };
    if (!this.assets.isEmpty()) {
      map.set(
        "assets",
        JSON.Array(Vector::createFromItems(this.assets.map(v -> v.toJSON()))),
      )
    };
    if (this.sideEffects) {
      map.set("sideEffects", JSON.Bool(this.sideEffects))
    };
    JSON.Object(map.chill());
  }
}

module end;
