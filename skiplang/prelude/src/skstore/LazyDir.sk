/*****************************************************************************/
/* Lazy directories. */
/*****************************************************************************/
module SKStore;

class CyclicExn(Key) extends Exception, File {
  fun toString(): String {
    inspect(this).toString()
  }
}

base class LazyResult {
  children =
  | LDefined(Array<File>, Array<Path>)
  | LRemoved(Array<Path>)
  | LCycle()
  | LAbsent()
}

class LazyDir private {
  time: Time,
  timeStack: TimeStack,
  dirName: DirName,
  data: SortedMap<Key, LazyResult> = SortedMap[],
  private lazyFun: ((mutable Context, Key) ~> LazyResult),
  collect: Bool,
} extends Dir {
  static fun create(
    context: mutable Context,
    dirName: DirName,
    f: (mutable Context, DirName, Key) ~> ?Array<File>,
    collect: Bool = true,
  ): Dir {
    time = context.timeStamp();
    lazyFun = (context: mutable Context, key: Key) ~> {
      dir = context.unsafeGetLazyDir(dirName);
      !dir.data[key] = LCycle();
      context.setDir(dir);

      readsCopy = context.reads;
      context.!reads = SortedSet[];

      resultOpt = try {
        f(context, dirName, key)
      } catch {
      | exn ->
        !dir = context.unsafeGetLazyDir(dirName);
        !dir.data[key] = LAbsent();
        context.setDir(dir);
        context.!reads = readsCopy;
        throw exn
      };

      result = resultOpt match {
      | None() -> LAbsent()
      | Some(x) -> LDefined(x, context.reads.toArray())
      };

      for (read in context.reads) {
        arrowKey = ArrowKey(dirName, dirName, key);
        context.!deps = context.deps.set(read, arrowKey);
      };

      !dir = context.unsafeGetLazyDir(dirName);
      !dir.data[key] = result;
      context.setDir(dir);

      if (context.debugMode) {
        name = "" + dir.dirName + key;
        print_string(`WRITTEN LAZY:  ${name}`);
      };
      context.!reads = readsCopy;
      result
    };
    context.maybeGetLazyDir(dirName) match {
    | Some(ldir) if (native_eq(ldir.lazyFun, lazyFun) == 0) ->
      if (context.debugMode) {
        print_string(`REUSING: ${dirName.toString()}`);
      };
      ldir
    | _ ->
      newDir = LazyDir{
        time,
        timeStack => TimeStack::create(context, time),
        dirName,
        lazyFun,
        collect,
      };
      updateDirtyReaders(context, Path::create(dirName.tag(), DirTag()));
      context.setDir(newDir);
      newDir
    }
  }

  fun withData(data: SortedMap<Key, LazyResult>): this {
    this with {data}
  }

  fun getTime(): Time {
    this.time
  }

  fun getTimeStack(): TimeStack {
    this.timeStack
  }

  fun reset(context: mutable Context): void {
    context.setDir(this);
  }

  fun keys(): SortedSet<Key> {
    keys = SortedSet[];
    for (newKey => value in this.data) {
      if (value is LDefined _) {
        !keys = keys.set(newKey);
      };
    };
    keys
  }

  fun getDirName(): DirName {
    this.dirName
  }

  fun getArrayRaw(key: Key): Array<File> {
    this.data.maybeGet(key) match {
    | Some(LDefined(arr, _)) -> arr
    | _ -> Array[]
    }
  }

  fun update(
    context: mutable Context,
    dirtyReadersOpt: ?readonly Map<DirName, SortedSet<Key>>,
  ): void {
    dirtyReaders = SortedSet[];
    firstDirtyReaders = dirtyReadersOpt match {
    | None() -> mutable Map[]
    | Some(m) -> m
    };

    for (dirtyReaderDirName => dirtyReaderKeys in firstDirtyReaders) {
      for (dirtyReaderKey in dirtyReaderKeys) {
        !dirtyReaders = dirtyReaders.set(
          Path::create(dirtyReaderDirName, dirtyReaderKey),
        );
      }
    };

    for (path in dirtyReaders) {
      key = path.baseName;
      oldValue = this.data.maybeGet(key);
      !this.data = this.data.remove(key);
      updateDirtyReaders(context, path);
      oldValue match {
      | Some(LDefined(_, reads))
      | Some(LRemoved(reads)) ->
        for (read in reads) {
          arrow = ArrowKey(this.dirName, this.dirName, key);
          context.!deps = context.deps.remove(read, arrow)
        }
      | _ -> void
      }
    };

    context.setDir(this);
  }

  fun unsafeGetArray(
    context: mutable Context,
    key: Key,
    throwOnCycle: Bool = true,
  ): Array<File> {
    if (this.collect) {
      context.!lazyGets = context.lazyGets.set(Path::create(this.dirName, key));
    };
    lazyResult = this.data.maybeGet(key) match {
    | Some(LCycle()) if (throwOnCycle) -> throw CyclicExn(key)
    | x @ None()
    | x @ Some(LRemoved _)
    | x @ Some(LAbsent()) ->
      if (x is Some(LRemoved _) && context.debugMode) {
        print_string("LRemoved: " + Path::create(this.dirName, key).toString())
      };
      arrow = context.currentArrow() match {
      | Some(a) -> Some(ArrowKey(a.childName, this.dirName, key))
      | _ -> None()
      };
      arrow.each(a -> context.enter(a, this.timeStack));
      res = this.lazyFun(context, key);
      arrow.each(context.leave);
      res
    | Some(x) -> x
    };
    lazyResult match {
    | LDefined(arr, _reads) -> arr
    | _ -> Array[]
    }
  }

  fun getArray(context: mutable Context, key: Key): Array<File> {
    this.getArrayWithOptions(context, key, true)
  }

  fun getArrayWithOptions(
    context: mutable Context,
    key: Key,
    throwOnCycle: Bool = true,
  ): Array<File> {
    path = Path::create(this.dirName, key);
    context.addDep(path);
    this.unsafeGetArray(context, key, throwOnCycle)
  }

  fun getIter(context: mutable Context, key: Key): mutable Iterator<File> {
    this.getArray(context, key).iterator()
  }

  fun getArrayAsync(context: mutable Context, key: Key): Array<File> {
    path = Path::create(this.dirName, key);
    context.addDep(path);
    this.unsafeGetArray(context, key, true)
  }
}

module end;
