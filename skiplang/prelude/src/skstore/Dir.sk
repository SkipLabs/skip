/*****************************************************************************/
/* The interface for directories. */
/*****************************************************************************/

module SKStore;

base class Dir protected {time: Time, timeStack: TimeStack, dirName: DirName} {
  /**
   * Returns an array of files associated with a key.
   * The array is empty if the key does not exist.
   */
  fun getArray(context: mutable Context, key: Key): Array<File>;

  /**
   * Returns an iterator of files associated with a key.
   * The iterator is empty if the key does not exist.
   */
  fun getIter(context: mutable Context, key: Key): mutable Iterator<File>;

  /**
   * Same as getArray but does not create a dependency, which will break in
   * incremental mode. This should only be used for testing.
   */
  fun getArrayRaw(key: Key): Array<File>;

  /**
   * Returns the list of files in a directory. Should only be used for testing.
   */
  fun keys(): SortedSet<Key>;

  /**
   * Returns the directory name
   */
  fun getDirName(): DirName {
    this.dirName
  }

  /**
   * Returns the creation time of that directory.
   */
  fun getTime(): Time {
    this.time
  }

  /**
   * Returns the creation time of that directory and all of its ancestors
   */
  fun getTimeStack(): TimeStack {
    this.timeStack
  }
}

/*****************************************************************************/
/* Exceptions */
/*****************************************************************************/

class DirAlreadyExists(dirName: DirName) extends Exception {
  fun toString(): String {
    `Directory ${this.dirName} already exists`
  }
}

module end;
