module Posix;

@debug
@cpp_extern("SKIP_posix_open")
native fun internalOpen(path: String, oflag: Int, mode: Int): Int;

fun open(path: String, oflag: Int, mode: Int = 0): Int {
  internalOpen(path, oflag, mode)
}

@debug
@cpp_extern("SKIP_posix_close")
native fun close(fd: Int): void;

@debug
@cpp_extern("SKIP_posix_write")
native fun write(fd: Int, buf: readonly Unsafe.Ptr<UInt8>, len: Int): Int;

@debug
@cpp_extern("SKIP_posix_read")
native fun read(fd: Int, buf: mutable Unsafe.Ptr<UInt8>, len: Int): Int;

@debug
@cpp_extern("SKIP_posix_lseek")
native fun internalLseek(fd: Int, offset: Int, whence: Int): Int;

fun lseek(fd: Int, offset: Int, whence: SeekWhence): Int {
  internalLseek(fd, offset, whence.toInt())
}

@debug
@cpp_extern("SKIP_posix_pipe")
native fun pipe(): Pipe;

@debug
@cpp_extern("SKIP_posix_dup")
native fun dup(fd: Int): Int;

@debug
@cpp_extern("SKIP_posix_dup2")
native fun dup2(oldfd: Int, newfd: Int): void;

@cpp_extern("SKIP_posix_open_flags")
native fun open_flags(
  read: Int,
  write: Int,
  append: Int,
  truncate: Int,
  create: Int,
  create_new: Int,
): Int;

@cpp_extern("SKIP_posix_wifexited")
native fun wifexited(stat_loc: Int): Bool;
@cpp_extern("SKIP_posix_wifsignaled")
native fun wifsignaled(stat_loc: Int): Bool;
@cpp_extern("SKIP_posix_wifstopped")
native fun wifstopped(stat_loc: Int): Bool;
@cpp_extern("SKIP_posix_wexitstatus")
native fun wexitstatus(stat_loc: Int): Int;
@cpp_extern("SKIP_posix_wtermsig")
native fun wtermsig(stat_loc: Int): Int;
@cpp_extern("SKIP_posix_wstopsig")
native fun wstopsig(stat_loc: Int): Int;

@debug
@cpp_extern("SKIP_posix_kill")
native fun kill(pid: Int, sig: Int): void;

@debug
@cpp_extern("SKIP_posix_waitpid")
native fun waitpid(pid: Int, nohang: Bool = false): Int;

@debug
@cpp_extern("SKIP_posix_execvp")
native fun internalExecvp(args: Array<String>): void;

fun execvp<T>(args: Array<String>): T {
  internalExecvp(args);
  invariant_violation("unreachable")
}

@debug
@cpp_extern("SKIP_posix_poll")
native fun poll(pollfds: mutable Array<mutable Pollfd>): Int;

@debug
@cpp_extern("SKIP_posix_isatty")
native fun internalIsatty(fd: Int): Int;
fun isatty(fd: Int): Bool {
  internalIsatty(fd) != 0
}

@debug
@cpp_extern("SKIP_posix_mkstemp")
native fun internalMkstemp(template: String): Int;

fun mkstemp(template: String): mutable IO.File {
  // TODO: Properly set filename.
  mutable IO.File(internalMkstemp(template))
}

class Pipe(output: Int, input: Int) {}

@cpp_export("sk_create_posix_pipe")
fun internalCreatePipe(output_fd: Int, input_fd: Int): Pipe {
  Pipe(output_fd, input_fd)
}

base class SeekWhence {
  children =
  | SeekSet()
  | SeekCur()
  | SeekEnd()
  | SeekHole()
  | SeekData()

  fun toInt(): Int
  | SeekSet() -> 0
  | SeekCur() -> 1
  | SeekEnd() -> 2
  | SeekHole() -> 3
  | SeekData() -> 4
}

base class WExitStatus {
  children =
  | WExited(status: Int)
  | WSignaled(sig: Int)
  | WStopped(sig: Int)

  static fun create(stat_loc: Int): WExitStatus {
    if (wifexited(stat_loc)) {
      WExited(wexitstatus(stat_loc))
    } else if (wifsignaled(stat_loc)) {
      WSignaled(wtermsig(stat_loc))
    } else { // wifstopped(stat_loc)
      WStopped(wstopsig(stat_loc))
    }
  }

  fun success(): Bool
  | WExited(status) -> (status == 0)
  | _ -> false

  fun exitcode(): Int
  | WExited(status) -> status
  | _ -> -1
}

base class Signal {
  children =
  | SIGHUP()
  | SIGINT()
  | SIGQUIT()
  | SIGILL()
  | SIGTRAP()
  | SIGABRT()
  | SIGEMT()
  | SIGFPE()
  | SIGKILL()
  | SIGBUS()
  | SIGSEGV()
  | SIGSYS()
  | SIGPIPE()
  | SIGALRM()
  | SIGTERM()
  | SIGURG()
  | SIGSTOP()
  | SIGTSTP()
  | SIGCONT()
  | SIGCHLD()
  | SIGTTIN()
  | SIGTTOU()
  | SIGIO()
  | SIGXCPU()
  | SIGXFSZ()
  | SIGVTALRM()
  | SIGPROF()
  | SIGWINCH()
  | SIGINFO()
  | SIGUSR1()
  | SIGUSR2()

  fun code(): Int
  | SIGHUP() -> 1
  | SIGINT() -> 2
  | SIGQUIT() -> 3
  | SIGILL() -> 4
  | SIGTRAP() -> 5
  | SIGABRT() -> 6
  | SIGEMT() -> 7
  | SIGFPE() -> 8
  | SIGKILL() -> 9
  | SIGBUS() -> 10
  | SIGSEGV() -> 11
  | SIGSYS() -> 12
  | SIGPIPE() -> 13
  | SIGALRM() -> 14
  | SIGTERM() -> 15
  | SIGURG() -> 16
  | SIGSTOP() -> 17
  | SIGTSTP() -> 18
  | SIGCONT() -> 19
  | SIGCHLD() -> 20
  | SIGTTIN() -> 21
  | SIGTTOU() -> 22
  | SIGIO() -> 23
  | SIGXCPU() -> 24
  | SIGXFSZ() -> 25
  | SIGVTALRM() -> 26
  | SIGPROF() -> 27
  | SIGWINCH() -> 28
  | SIGINFO() -> 29
  | SIGUSR1() -> 30
  | SIGUSR2() -> 31

  fun toString(): String
  | SIGHUP() -> "SIGHUP"
  | SIGINT() -> "SIGINT"
  | SIGQUIT() -> "SIGQUIT"
  | SIGILL() -> "SIGILL"
  | SIGTRAP() -> "SIGTRAP"
  | SIGABRT() -> "SIGABRT"
  | SIGEMT() -> "SIGEMT"
  | SIGFPE() -> "SIGFPE"
  | SIGKILL() -> "SIGKILL"
  | SIGBUS() -> "SIGBUS"
  | SIGSEGV() -> "SIGSEGV"
  | SIGSYS() -> "SIGSYS"
  | SIGPIPE() -> "SIGPIPE"
  | SIGALRM() -> "SIGALRM"
  | SIGTERM() -> "SIGTERM"
  | SIGURG() -> "SIGURG"
  | SIGSTOP() -> "SIGSTOP"
  | SIGTSTP() -> "SIGTSTP"
  | SIGCONT() -> "SIGCONT"
  | SIGCHLD() -> "SIGCHLD"
  | SIGTTIN() -> "SIGTTIN"
  | SIGTTOU() -> "SIGTTOU"
  | SIGIO() -> "SIGIO"
  | SIGXCPU() -> "SIGXCPU"
  | SIGXFSZ() -> "SIGXFSZ"
  | SIGVTALRM() -> "SIGVTALRM"
  | SIGPROF() -> "SIGPROF"
  | SIGWINCH() -> "SIGWINCH"
  | SIGINFO() -> "SIGINFO"
  | SIGUSR1() -> "SIGUSR1"
  | SIGUSR2() -> "SIGUSR2"

  static fun fromCode(code: Int): ?Signal {
    code match {
    | 1 -> Some(SIGHUP())
    | 2 -> Some(SIGINT())
    | 3 -> Some(SIGQUIT())
    | 4 -> Some(SIGILL())
    | 5 -> Some(SIGTRAP())
    | 6 -> Some(SIGABRT())
    | 7 -> Some(SIGEMT())
    | 8 -> Some(SIGFPE())
    | 9 -> Some(SIGKILL())
    | 10 -> Some(SIGBUS())
    | 11 -> Some(SIGSEGV())
    | 12 -> Some(SIGSYS())
    | 13 -> Some(SIGPIPE())
    | 14 -> Some(SIGALRM())
    | 15 -> Some(SIGTERM())
    | 16 -> Some(SIGURG())
    | 17 -> Some(SIGSTOP())
    | 18 -> Some(SIGTSTP())
    | 19 -> Some(SIGCONT())
    | 20 -> Some(SIGCHLD())
    | 21 -> Some(SIGTTIN())
    | 22 -> Some(SIGTTOU())
    | 23 -> Some(SIGIO())
    | 24 -> Some(SIGXCPU())
    | 25 -> Some(SIGXFSZ())
    | 26 -> Some(SIGVTALRM())
    | 27 -> Some(SIGPROF())
    | 28 -> Some(SIGWINCH())
    | 29 -> Some(SIGINFO())
    | 30 -> Some(SIGUSR1())
    | 31 -> Some(SIGUSR2())
    | _ -> None()
    }
  }
}

mutable class Process(pid: Int) {
  mutable fun kill(sig: Signal = SIGKILL()): void {
    kill(this.pid, sig.code())
  }

  mutable fun poll(): ?WExitStatus {
    waitpid(this.pid, /* nohang = */ true) match {
    | -1 -> None()
    | stat_loc -> Some(WExitStatus::create(stat_loc))
    }
  }

  mutable fun wait(): WExitStatus {
    WExitStatus::create(waitpid(this.pid))
  }
}

// TODO: Properly handle redirecting to /dev/null.
mutable class Popen(
  process: mutable Process,
  stdin: ?mutable IO.File,
  stdout: ?mutable IO.File,
  stderr: ?mutable IO.File,
) {
  static fun create{
    args: Array<String>,
    env: Map<String, String> = Map[],
    clear_env: Bool = false,
    cwd: ?String = None(),
    stdin: Bool = false,
    stdout: Bool = false,
    stderr: Bool = false,
  }: Result<mutable Popen, IO.Error> {
    file_actions = SpawnFileActions::init();
    stdin_pipe = if (stdin) Some(pipe()) else None();
    stdout_pipe = if (stdout) Some(pipe()) else None();
    stderr_pipe = if (stderr) Some(pipe()) else None();
    stdin_pipe.each(pipe -> {
      file_actions.dup2(pipe.output, IO.stdin().fileno);
      file_actions.close(pipe.input);
      file_actions.close(pipe.output)
    });
    stdout_pipe.each(pipe -> {
      file_actions.dup2(pipe.input, IO.stdout().fileno);
      file_actions.close(pipe.input);
      file_actions.close(pipe.output)
    });
    stderr_pipe.each(pipe -> {
      file_actions.dup2(pipe.input, IO.stderr().fileno);
      file_actions.close(pipe.input);
      file_actions.close(pipe.output)
    });

    !env = if (clear_env) {
      env
    } else {
      e = Map::mcreateFromIterator(Environ.vars());
      e.extend(env);
      e.chill()
    };
    new_env = env.items().map(e -> `${e.i0}=${e.i1}`).collect(Array);

    // NOTE: Since `posix_spawn()` does not provide a way for changing the
    // working directory of the child process, we rely on GNU env.
    cwd match {
    | Some(path) -> !args = Array["env", "-C", path].concat(args)
    | None() -> void
    };

    p = spawnp(args, new_env, file_actions);
    file_actions.destroy();

    stdin_pipe.each(pipe -> close(pipe.output));
    stdout_pipe.each(pipe -> close(pipe.input));
    stderr_pipe.each(pipe -> close(pipe.input));

    p.map(p ->
      mutable Popen(
        p,
        stdin_pipe.map(pipe -> mutable IO.File(pipe.input)),
        stdout_pipe.map(pipe -> mutable IO.File(pipe.output)),
        stderr_pipe.map(pipe -> mutable IO.File(pipe.output)),
      )
    )
  }

  mutable fun kill(sig: Signal = SIGKILL()): void {
    this.process.kill(sig)
  }

  mutable fun poll(): ?WExitStatus {
    res = this.process.poll();
    if (res.isSome()) {
      this.closefds()
    };
    res
  }

  mutable fun wait(): WExitStatus {
    res = this.process.wait();
    this.closefds();
    res
  }

  private mutable fun closefds(): void {
    this.stdin.each(f -> f.close());
    this.stdout.each(f -> f.close());
    this.stderr.each(f -> f.close())
  }
}

base class PollEvents {
  children =
  | POLLIN()
  | POLLERR()
  | POLLHUP()
  // TODO: Complete set of events.

  // TODO: Properly derive those values from poll.h.
  fun value(): Int16
  | POLLIN() -> Int16::truncate(1)
  | POLLERR() -> Int16::truncate(8)
  | POLLHUP() -> Int16::truncate(16)
}

// TODO: Use proper integer type depending on platform.
mutable value class Pollfd(fd: Int32, events: Int16, revents: Int16) {
  static fun create(
    fd: Int,
    events: Int16 = POLLIN().value(),
    revents: Int16 = Int16::truncate(0),
  ): mutable Pollfd {
    mutable Pollfd(Int32::truncate(fd), events, revents)
  }

  readonly fun pollin(): Bool {
    // NOTE: Unnecessary toInt() because Int32 lacks bitwise operators.
    this.revents.toInt().and(POLLIN().value()) > 0
  }

  readonly fun pollerr(): Bool {
    this.revents.toInt().and(POLLERR().value()) > 0
  }

  readonly fun pollhup(): Bool {
    this.revents.toInt().and(POLLHUP().value()) > 0
  }
}

mutable native class SpawnFileActions {
  @cpp_extern("SKIP_posix_spawn_file_actions_init")
  native static fun init(): mutable SpawnFileActions;

  @cpp_extern("SKIP_posix_spawn_file_actions_destroy")
  mutable native fun destroy(): void;

  @cpp_extern("SKIP_posix_spawn_file_actions_adddup2")
  mutable native fun dup2(oldfd: Int, newfd: Int): void;

  @cpp_extern("SKIP_posix_spawn_file_actions_addclose")
  mutable native fun close(fd: Int): void;
}

@cpp_extern("SKIP_posix_spawnp")
native fun internalSpawnp(
  argv: Array<String>,
  env: Array<String>,
  file_actions: readonly SpawnFileActions,
): Int;

fun spawnp(
  argv: Array<String>,
  env: Array<String>,
  file_actions: readonly SpawnFileActions,
): Result<mutable Process, IO.Error> {
  pid = internalSpawnp(argv, env, file_actions);
  if (pid < 0) {
    return Failure(IO.Error(-pid))
  };

  Success(mutable Process(pid))
}

module end;
