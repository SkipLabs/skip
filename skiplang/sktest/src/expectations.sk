module SKTest;

class ExpectationError(
  msg: String,
  expected: String,
  actual: String,
) extends Exception {
  fun getMessage(): String {
    this.msg + "\n\texpected: " + this.expected + "\n\tactual: " + this.actual
  }
}

fun expectCmp<T: frozen>(
  actual: T,
  expected: T,
  f: (T, T) -> Bool,
  msg: String = "expected successful comparison",
): void {
  if (!f(actual, expected)) {
    throw ExpectationError(
      msg,
      inspect(expected).toString(),
      inspect(actual).toString(),
    )
  }
}

fun expectEq<T: frozen & Equality>(
  actual: T,
  expected: T,
  msg: String = "expected equality",
): void {
  expectCmp(actual, expected, isEqual, msg)
}

fun expectNe<T: frozen & Equality>(
  actual: T,
  expected: T,
  msg: String = "expected difference",
): void {
  expectCmp(actual, expected, isNotEqual, msg)
}

fun expectTrue(b: Bool, msg: String = "expected true"): void {
  if (!b) {
    throw ExpectationError(msg, "true", "false")
  }
}

fun expectFalse(b: Bool, msg: String = "expected false"): void {
  if (b) {
    throw ExpectationError(msg, "false", "true")
  }
}

fun fail<T>(msg: String = "failure"): T {
  throw ExpectationError(msg, "", "")
}

fun expectThrow(f: () -> void, msg: String = "expected throw"): void {
  success = false;
  try {
    f()
  } catch {
  | _ -> !success = true
  };
  if (!success) {
    fail(msg)
  }
}

module end;
