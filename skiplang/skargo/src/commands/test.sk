module Skargo;

fun nproc(): Int {
  p = System.subprocess(Array["nproc"]).fromSuccess();
  p.exitstatus match {
  | Posix.WExited(0) ->
    p.stdout.trim().toIntOption() match {
    | Some(n) -> n
    | None() -> 8
    }
  | _ -> 8
  };
}

fun test(): (Cli.Command, (GlobalContext, Cli.ParseResults) ~> void) {
  (
    Cli.Command("test")
      .short("t")
      .about("Run the tests")
      .arg(
        Cli.Arg::string("manifest-path")
          .value_name("PATH")
          .about("Path to Skargo.toml"),
      )
      .arg(
        Cli.Arg::string("filter")
          .positional()
          .about("If specified, only run tests with names matching the filter"),
      )
      .arg(
        Cli.Arg::int("jobs")
          .long("jobs")
          .short("j")
          .default(nproc())
          .value_name("N")
          .about("Number of parallel jobs, defaults to # of CPUs"),
      )
      .arg(
        Cli.Arg::string("target")
          .value_name("TRIPLE")
          .about("Check for the target triple"),
      )
      .arg(
        Cli.Arg::string("target-dir")
          .value_name("PATH")
          .about("Directory for all generated artifacts"),
      )
      .args(kProfileArgs)
      .arg(Cli.Arg::bool("list").about("Display the list of available tests"))
      .arg(
        Cli.Arg::string("junitxml")
          .value_name("PATH")
          .about("Generate a JUnit XML report"),
      )
      .arg(kSkcOptArg),
    execTest,
  )
}

fun execTest(gctx: GlobalContext, args: Cli.ParseResults): void {
  ws = workspace(args, gctx);
  opts = compile_options(args);

  // In dev mode, if the package has both a bin and test target, use the
  // merged binary (bin target includes test code via dispatcher).
  bins = ws.package.manifest.targets
    .filter(target -> target.kind is BinTarget _)
    .collect(Array);
  merged =
    opts.build_config.requested_profile == "dev" &&
    ws.package.manifest.test_target().isSome() &&
    bins.size() == 1;

  if (merged) {
    !opts.filter = CompileFilter::single_bin(bins[0].name)
  } else {
    !opts.filter = CompileFilter::all_tests()
  };

  bctx = create_bctx(gctx, ws, opts);
  withTimer(
    () -> {
      build_runner = BuildRunner::create(bctx);
      build_runner.compile()
    },
    t ->
      gctx.console.status(
        "Finished",
        `${opts.build_config.requested_profile} target(s) in ${t}s`,
      )
    ,
  );

  // TODO: Pass args directly after --.
  filter = args.maybeGetString("filter");
  junitxml = args.maybeGetString("junitxml").map(f -> `--junitxml=${f}`);
  jobs = args.getInt("jobs");
  list = if (args.getBool("list")) Some("--list") else None();

  optBinary = if (merged) {
    unit = bctx.roots.find(r -> r.target.kind is BinTarget _);
    unit match {
    | Some(u) ->
      path = Path.join(
        ws.target_dir(),
        "host",
        opts.build_config.requested_profile,
      );
      Some(Path.join(path, u.target.name))
    | None() ->
      gctx.console.error("`skargo test` could not find bin target.");
      skipExit(1)
    }
  } else if (
    bctx.roots.size() != 1 ||
    !(bctx.roots[0].target.kind is TestTarget _)
  ) {
    gctx.console.error("`skargo test` could not find test target.");
    skipExit(1)
  } else {
    unit = bctx.roots[0];
    unit.arch match {
    | TargetArchTriple(t) if (t.isWasm32()) -> None()
    | _ -> // TODO: Expose common API with BuildRunner.
      path = Path.join(
        ws.target_dir(),
        "host",
        opts.build_config.requested_profile,
      );
      Some(Path.join(path, unit.target.name))
    }
  };
  optBinary match {
  | Some(binary) ->
    if (!FileSystem.exists(binary)) {
      gctx.console.error(
        `\`skargo test\` could not find test binary ${binary}.`,
      );
      skipExit(1)
    };
    if (merged) {
      Environ.set_var("SKTEST", "1")
    };
    cmd_args = Array[binary].concat(
      Array[junitxml, filter, list, Some(`-j${jobs}`)].filterNone(),
    );
    Posix.execvp(cmd_args)
  | _ -> void
  }
}

module end;
