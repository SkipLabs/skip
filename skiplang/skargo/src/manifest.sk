module Skargo;

base class LibraryType uses Equality, Hashable {
  children =
  | LibraryTypeSklib()
  | LibraryTypeCdylib()

  static fun fromString(str: String): LibraryType {
    str match {
    | "sklib" -> LibraryTypeSklib()
    | "cdylib" -> LibraryTypeCdylib()
    // TODO: Proper error.
    | _ -> invariant_violation(`Unrecognized library type '${str}'.`)
    }
  }
}

class TomlLibrary{type: Array<LibraryType>} {
  static fun fromTOML(toml: TOML.Table): TomlLibrary {
    type = toml.maybeGetArray("type") match {
    | Some(arr) ->
      arr.map(s -> LibraryType::fromString(s.expectString())).collect(Array)
    | None() -> Array[]
    };

    TomlLibrary{type}
  }

  static fun default(): TomlLibrary {
    TomlLibrary{type => Array[LibraryTypeSklib()]}
  }
}

// TODO: Add support for `git` repositories as a package location.
class TomlDependency{version: ?String = None(), path: ?String = None()} {
  static fun fromTOML(basePath: String, toml: TOML.Value): TomlDependency {
    toml match {
    | TOML.String(v) -> TomlDependency{version => Some(v)}
    | t @ TOML.Table _ ->
      t.maybeGetString("path") match {
      | Some(path) ->
        TomlDependency{
          // FIXME: This breaks on absolute paths.
          path => Some(Path.join(basePath, path)),
          version => t.maybeGetString("version"),
        }
      | _ -> invariant_violation("TODO")
      }
    | _ -> invariant_violation("Invalid dependency specification.")
    }
  }
}

class Dependency(
  name: String,
  source_id: SourceId,
  req: Semver.VersionReq,
  kind: DepKind,
) {
  static fun create(
    name: String,
    source_id: SourceId,
    version: ?String,
    kind: DepKind,
  ): Dependency {
    version_req = version match {
    | Some(req) -> Semver.VersionReq::fromString(req)
    | None() -> Semver.VersionReq::kStar
    };

    Dependency(name, source_id, version_req, kind)
  }

  static fun fromTomlDependency(
    toml_name: String,
    dep: TomlDependency,
    kind: DepKind,
  ): Dependency {
    source_id = (dep.path, dep.version) match {
    | (None(), None()) ->
      invariant_violation(
        `dependency ${toml_name} specified without providing a local path or a version.`,
      )
    | (Some(path), _) -> SourceId::from_path(path)
    | (None(), Some(_version)) ->
      invariant_violation("TODO: Package registries not implemented yet")
    };

    Dependency::create(toml_name, source_id, dep.version, kind)
  }
}

class SourceId(url: String, kind: SourceKind) uses Equality {
  static fun from_path(path: String): SourceId {
    SourceId(path, PathSource())
  }
}

base class SourceKind uses Equality {
  children =
  | PathSource()
  // | GitSource()
  // | RegistrySource()
}

base class DepKind {
  children =
  | NormalDep()
  | BuildDep()
  | DevelopmentDep()
}

class TomlBinary(name: String, main: String) uses Equality {
  static fun fromTOML(toml: TOML.Table): TomlBinary {
    name = toml.getString("name");
    main = toml.getString("main");

    TomlBinary(name, main)
  }
}

class TomlManifest{
  name: String,
  version: String,
  tests: ?Array<String>,
  test_harness: ?String,
  dependencies: Map<String, TomlDependency>,
  dev_dependencies: Map<String, TomlDependency>,
  build_dependencies: Map<String, TomlDependency>,
  bin: Array<TomlBinary>,
  lib: TomlLibrary,
} {
  static fun read(path: String): TomlManifest {
    basePath = Path.dirname(path);
    toml = TOML.decode(FileSystem.readTextFile(path));
    package = toml.getTable("package");
    name = package.getString("name");
    version = package.getString("version");

    test_harness = package.maybeGetString("test-harness");

    tests = package.maybeGetArray("tests").map(arr ->
      arr.map(x -> x.expectString())
    );

    dependencies = toml.maybeGetTable("dependencies") match {
    | Some(TOML.Table(t)) ->
      t.map((_, v) -> TomlDependency::fromTOML(basePath, v))
    | None() -> Map[]
    };

    dev_dependencies = toml.maybeGetTable("dev-dependencies") match {
    | Some(TOML.Table(t)) ->
      t.map((_, v) -> TomlDependency::fromTOML(basePath, v))
    | None() -> Map[]
    };

    build_dependencies = toml.maybeGetTable("build-dependencies") match {
    | Some(bDependencies) ->
      bDependencies.map.map((_, v) -> TomlDependency::fromTOML(basePath, v))
    | _ -> Map[]
    };

    lib = toml.maybeGetTable("lib") match {
    | Some(t) -> TomlLibrary::fromTOML(t)
    | None() -> TomlLibrary::default()
    };

    bin = toml.maybeGetArray("bin") match {
    | Some(arr) -> arr.map(t -> TomlBinary::fromTOML(t.expectTable()))
    | None() -> Array[]
    };

    TomlManifest{
      name,
      version,
      tests,
      test_harness,
      dependencies,
      dev_dependencies,
      build_dependencies,
      lib,
      bin,
    }
  }
}

class Manifest(
  package_id: PackageId,
  dependencies: Array<Dependency>,
  targets: Array<Target>,
  original: TomlManifest,
) {
  static fun read(path: String): Manifest {
    static::fromTomlManifest(TomlManifest::read(path), path)
  }

  static fun fromTomlManifest(
    manifest: TomlManifest,
    manifest_path: String,
  ): Manifest {
    package_root = Path.dirname(manifest_path);
    source_id = SourceId::from_path(package_root);
    package_id = PackageId(
      manifest.name,
      Semver.Version::fromString(manifest.version),
      source_id,
    );
    dependencies = manifest.dependencies
      .map((toml_name, toml_dep) ->
        Dependency::fromTomlDependency(toml_name, toml_dep, NormalDep())
      )
      .values()
      .collect(Array);
    dev_dependencies = manifest.dev_dependencies
      .map((toml_name, toml_dep) ->
        Dependency::fromTomlDependency(toml_name, toml_dep, DevelopmentDep())
      )
      .values()
      .collect(Array);
    build_dependencies = manifest.build_dependencies
      .map((toml_name, toml_dep) ->
        Dependency::fromTomlDependency(toml_name, toml_dep, BuildDep())
      )
      .values()
      .collect(Array);
    targets = manifest_targets(manifest, package_root);

    Manifest(
      package_id,
      dependencies.concat(dev_dependencies).concat(build_dependencies),
      targets,
      manifest,
    )
  }

  fun lib_target(): ?Target {
    this.targets.find(t -> t.kind == LibTarget(LibraryTypeSklib()))
  }

  fun build_script_target(): ?Target {
    this.targets.find(t -> t.is_build_script())
  }

  fun test_target(): ?Target {
    this.targets.find(t -> t.is_test())
  }
}

class PackageId(
  name: String,
  version: Semver.Version,
  source_id: SourceId,
) uses Equality, Hashable {
  // TODO: Proper hash
  fun hash(): Int {
    `${this.name}@${this.version}`.hash()
  }
}

class Target(
  kind: TargetKind,
  name: String,
  srcs: Array<String>,
) uses Equality, Hashable {
  fun hash(): Int {
    (this.kind, this.name, this.srcs).hash()
  }

  fun is_lib(): Bool {
    this.kind is LibTarget _
  }

  fun is_bin(): Bool {
    this.kind is BinTarget _
  }

  fun is_build_script(): Bool {
    this.kind is CustomBuildTarget _
  }

  fun is_test(): Bool {
    this.kind is TestTarget _
  }
}

base class TargetKind uses Equality, Hashable {
  children =
  | LibTarget(type: LibraryType)
  | BinTarget(entry_point: String)
  | TestTarget(entry_point: String)
  | CustomBuildTarget()
}

private fun manifest_targets(
  manifest: TomlManifest,
  package_root: String,
): Array<Target> {
  res = mutable Vector[];

  srcs = FileSystem.readFilesRecursive(Path.join(package_root, "src"), f ->
    f.endsWith(".sk")
  )
    .map(f -> f.stripPrefix(package_root + "/"))
    .toArray();

  // TODO: Only do this for explicitly specified `[lib]` sections.
  res.push(Target(LibTarget(LibraryTypeSklib()), "lib", srcs));
  if (manifest.lib.type.any(t -> t is LibraryTypeCdylib _)) {
    res.push(Target(LibTarget(LibraryTypeCdylib()), "cdylib", Array[]))
  };

  for (bin in manifest.bin) {
    res.push(Target(BinTarget(bin.main), bin.name, Array[]))
  };

  build_script_path = "build.sk";
  if (FileSystem.exists(Path.join(package_root, build_script_path))) {
    res.push(
      Target(
        CustomBuildTarget(),
        `build-script-${manifest.name}`,
        Array[build_script_path],
      ),
    )
  };

  test_srcs = manifest.tests match {
  | Some(tests) -> tests
  | _ ->
    tests_path = Path.join(package_root, "tests");
    if (FileSystem.exists(tests_path)) {
      FileSystem.readFilesRecursive(tests_path, f ->
        f.endsWith(".sk")
      ).toArray()
    } else {
      Array[]
    }
  };
  if (!test_srcs.isEmpty()) {
    test_harness = manifest.test_harness.default("SKTest.main");
    res.push(Target(TestTarget(test_harness), "test", test_srcs))
  };

  res.collect(Array)
}

module end;
