module Cli;

base class ArgumentError extends Exception uses Equality {
  children =
  | InvalidArgumentError(arg: String)
  | MissingArgumentError(arg: String)
  | MissingValueError(arg: String)
  | DuplicateValueError(arg: String)

  fun getMessage(): String
  | InvalidArgumentError(arg) -> `Invalid argument ${arg}.`
  | MissingArgumentError(arg) -> `Missing required argument ${arg}.`
  | MissingValueError(arg) -> `No value provided for argument ${arg}.`
  | DuplicateValueError(arg) -> `Multiple values provided for argument ${arg}.`
}

base class Value uses Show, Equality {
  children =
  | BoolValue(Bool)
  | StringValue(String)
  | MissingValue()

  fun toString(): String
  | MissingValue() -> "(no value)"
  | BoolValue(v) -> `boolean value: ${v}`
  | StringValue(v) -> `string value: ${v}`
}

class ParseResults{
  cmd: Command,
  values: Map<String, Value> = Map[],
  extra: Array<String> = Array[],
  subcommand: ?String = None(),
  error: ?ArgumentError = None(),
} {
  fun getBool(name: String): Bool {
    this.values[name] match {
    | BoolValue(v) -> v
    | _ -> invariant_violation(`Argument ${name} is not boolean.`)
    }
  }

  fun maybeGetString(name: String): ?String {
    this.values.maybeGet(name) match {
    | Some(StringValue(v)) -> Some(v)
    | Some(MissingValue()) -> None()
    | Some(_) -> invariant_violation(`Argument ${name} is not a string`)
    | None() -> invariant_violation(`Argument ${name} is undefined.`)
    }
  }

  fun getString(name: String): String {
    this.maybeGetString(name) match {
    | Some(s) -> s
    | None() -> invariant_violation(`Argument ${name} is missing.`)
    }
  }

  fun maybeGetSubcommand(): ?String {
    this.subcommand
  }

  fun getCommand(): Command {
    this.cmd
  }
}

fun parseArgs(cmd: Command): ParseResults {
  parseArgsFrom(cmd, arguments())
}

fun parseArgsFrom(cmd: Command, argv: Sequence<String>): ParseResults {
  parse(cmd, argv.values())
}

private fun parse(
  cmd: Command,
  iterator: mutable Iterator<String>,
  values: mutable Map<String, Value> = mutable Map<String, Value>[],
  globals: Array<Arg> = Array[],
): ParseResults {
  subcommands = mutable Map[];
  for (subcmd in cmd._subcommands) {
    if (subcommands.containsKey(subcmd.name)) {
      invariant_violation(`Duplicate subcommand ${subcmd.name}`)
    };
    subcommands.set(subcmd.name, subcmd);
    subcmd._short.each(n -> {
      if (subcommands.containsKey(n)) {
        invariant_violation(`Duplicate subcommand short alias ${n}`)
      };
      subcommands.set(n, subcmd)
    })
  };
  argsNames = mutable Set[];
  posArgs = mutable Vector[];
  // TODO: options is a bad name. Maybe flag_args?
  options = mutable Map[];
  for (arg in cmd._args.concat(globals)) {
    if (argsNames.contains(arg.name)) {
      invariant_violation(`Duplicate argument ${arg.name}`)
    };
    argsNames.add(arg.name);

    if (arg._positional) {
      posArgs.push(arg);
    } else {
      if (arg._short.isSome()) {
        options.add(`-${arg._short.fromSome()}`, arg);
        if (arg._long.isNone()) {
          if (arg._negatable) {
            invariant_violation(
              `Argument ${arg.name} cannot be negatable without a long name`,
            )
          };
          continue
        }
      };

      // Use arg name as default long flag for convenience.
      longFlag = arg._long.default(arg.name);
      options.add(`--${longFlag}`, arg);
      if (arg._negatable) {
        options.add(`--no-${longFlag}`, arg);
      };
    }
  };

  posArgId = 0;
  extra = mutable Vector[];
  hasExtra = false;
  error: ?ArgumentError = None();
  subcommand: ?String = None();

  loop {
    iterator.next() match {
    | None() -> break void
    | Some(str) if (hasExtra) -> extra.push(str)
    | Some(str) if (subcommands.containsKey(str)) ->
      subcmd = subcommands.get(str);
      !subcommand = Some(subcmd.name);

      // TODO: This does not support nested subcommands (which probably
      // shouldn't exist).
      globalArgs = cmd._args.filter(a -> a._global);
      res = parse(subcmd, iterator, values, globalArgs);
      if (res.error.isSome()) {
        !error = res.error;
        break void
      };

      // TODO: This is dirty.
      !extra = Vector::mcreateFromItems(res.extra);

      break void
    | Some("--") ->
      if (!cmd._extra) {
        !error = Some(InvalidArgumentError("--"));
        break void
      };
      !hasExtra = true
    | Some(str) ->
      (argName, argValue) = if (str.contains("=")) {
        (k, v) = str.splitFirst("=");
        (k, Some(v))
      } else {
        (str, None())
      };
      options.maybeGet(argName) match {
      | Some(arg) if (arg._takesValue()) ->
        value = argValue match {
        | Some(v) -> v
        | None() ->
          iterator.next() match {
          | Some(v) -> v
          | None() ->
            !error = Some(MissingValueError(arg.name));
            break void
          }
        };
        if (!values.containsKey(arg.name)) {
          values.set(arg.name, StringValue(value))
        } else {
          !error = Some(DuplicateValueError(arg.name));
          break void
        }
      | Some(arg) ->
        if (!values.containsKey(arg.name)) {
          values.set(arg.name, BoolValue(!str.startsWith("--no-")))
        } else {
          !error = Some(DuplicateValueError(arg.name));
          break void
        }
      | None() if (!str.startsWith("-") && posArgId < posArgs.size()) ->
        values.set(posArgs[posArgId].name, StringValue(str));
        !posArgId = posArgId + 1
      | None() ->
        !error = Some(InvalidArgumentError(str));
        break void
      }
    }
  };

  if (error.isNone()) {
    for (arg in cmd._args) {
      if (!values.containsKey(arg.name)) {
        if (arg._required) {
          !error = Some(MissingArgumentError(arg.name));
          break void
        } else {
          values.set(arg.name, arg._defaultValue())
        }
      }
    }
  };

  ParseResults{
    cmd => cmd,
    subcommand,
    values => values.chill(),
    extra => Array::createFromItems(extra.chill()),
    error => error,
  }
}
