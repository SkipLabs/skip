module alias T = SKTest;

module SKStoreTest;

@test
fun testLazy(): void {
  context = SKStore.run(context ~> {
    dirInput = context.mkdir(
      SKStore.IID::keyType,
      SKStore.IntFile::type,
      SKStore.DirName::create("/input/"),
      Array[(SKStore.IID(0), SKStore.IntFile(23))],
    );

    dirInput2 = context.mkdir(
      SKStore.IID::keyType,
      SKStore.IntFile::type,
      SKStore.DirName::create("/input2/"),
      Array[(SKStore.IID(0), SKStore.IntFile(44))],
    );
    dir1 = SKStore.LHandle::create(
      SKStore.IID::keyType,
      SKStore.IntFile::type,
      context,
      SKStore.DirName::create("/lazy1/"),
      (context, _self, x) ~> {
        Array[
          SKStore.IntFile(
            x match {
            | SKStore.IID(v) ->
              dirInput.getArray(context, SKStore.IID(0))[0].value + v
            },
          ),
        ]
      },
    );
    dir2 = SKStore.LHandle::create(
      SKStore.IID::keyType,
      SKStore.IntFile::type,
      context,
      SKStore.DirName::create("/lazy2/"),
      (context, _self, key) ~> {
        Array[SKStore.IntFile(dir1.getArray(context, key)[0].value)]
      },
    );

    adir1 = SKStore.LHandle::createAsync(
      SKStore.IID::keyType,
      SKStore.IntFile::type,
      context,
      SKStore.DirName::create("/alazy1/"),
      (context, _self, x) ~> {
        Some(
          Array[
            SKStore.IntFile(
              x match {
              | SKStore.IID(v) ->
                dirInput.getArray(context, SKStore.IID(0))[0].value + v
              },
            ),
          ],
        )
      },
    );
    _adir2 = SKStore.LHandle::createAsync(
      SKStore.IID::keyType,
      SKStore.IntFile::type,
      context,
      SKStore.DirName::create("/alazy2/"),
      (context, _self, key) ~> {
        arr = adir1.getArrayAsync(context, key);
        if (arr.size() == 0) None() else {
          Some(Array[SKStore.IntFile(arr[0].value)])
        }
      },
    );

    _dir3 = dirInput2.map(
      SKStore.IID::keyType,
      SKStore.IntFile::type,
      context,
      SKStore.DirName::create("/eager/"),
      (context, writer, key, _) ~> {
        writer.set(key, dir2.getArray(context, key)[0]);
      },
    )
  });

  input = SKStore.DirName::create("/input/");
  dir2 = SKStore.DirName::create("/lazy2/");
  adir2 = SKStore.DirName::create("/alazy2/");
  dir3 = SKStore.DirName::create("/eager/");

  T.expectEq(
    Array<SKStore.File>[],
    getLazyData(context, adir2, SKStore.IID(0), true),
    "Async Lazy basic",
  );

  _ = context.updateWithStatus(0);

  T.expectEq(
    Array<SKStore.File>[],
    getLazyData(context, adir2, SKStore.IID(0), true),
    "Async Lazy basic with interruption",
  );

  _ = context.updateWithStatus(30);

  T.expectEq(
    Array<SKStore.File>[SKStore.IntFile(23)],
    getLazyData(context, adir2, SKStore.IID(0), true),
    "Async Lazy basic without interruption",
  );

  T.expectEq(
    Array<SKStore.File>[SKStore.IntFile(23)],
    getData(context, dir2, SKStore.IID(0)),
    "Lazy basic",
  );
  write(context, input, SKStore.IID(0), Array[SKStore.IntFile(22)]);
  _status = context.updateWithStatus(10);

  T.expectEq(
    Array<SKStore.File>[SKStore.IntFile(22)],
    getData(context, dir2, SKStore.IID(0)),
    "Lazy after update",
  );

  T.expectEq(
    Array<SKStore.File>[SKStore.IntFile(22)],
    getData(context, dir3, SKStore.IID(0)),
    "Eeager after update",
  )
}

module end;
