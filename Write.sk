/*****************************************************************************/
/* Parsing the entries (used the option --write). */
/*****************************************************************************/

module SKFS;

/*****************************************************************************/
/* Escaping functions. */
/*****************************************************************************/

fun escape(str: String): String {
  output = mutable Vector[];
  for (c in str) {
    if (c == '\\') {
      output.push('\\');
      output.push('\\');
    } else if (c == '\t') {
      output.push('\\');
      output.push('t');
    } else if (c == '\n') {
      output.push('\\');
      output.push('n');
    } else if (c == '"') {
      output.push('\\');
      output.push('"');
    } else {
      output.push(c);
    }
  };
  String::fromChars(output.toArray())
}

fun unescape(input: readonly Vector<Char>): String {
  output = mutable Vector[];
  i = 0;
  while (i < input.size()) {
    input[i] match {
    | '\\' ->
      if (i + 1 >= input.size()) {
        output.push('\\');
        break void;
      };
      input[i + 1] match {
      | 't' ->
        output.push('\t');
        !i = i + 1
      | 'n' ->
        output.push('\n');
        !i = i + 1
      | '"' ->
        output.push('"');
        !i = i + 1
      | '\\' ->
        output.push('\\');
        !i = i + 1
      | _ -> output.push('\\')
      }
    | c -> output.push(c)
    };
    !i = i + 1;
  };
  String::fromChars(output.toArray())
}

fun getWriteKey(getChar: () -> Char): (?String, Bool) {
  chars = mutable Vector[];
  vtry<(?String, Bool)>(
    () -> {
      loop {
        c = getChar();
        if (c == '\\') {
          chars.push(c);
          continue
        };
        if (c == '\n') {
          return (None(), false);
        };
        if (c == '\t') {
          return (Some(unescape(chars)), false)
        };
        chars.push(c);
      }
    },
    _ -> return (None(), true),
  )
}

fun getWriteValue(getChar: () -> Char): ?String {
  chars = mutable Vector[];
  vtry<?String>(
    () -> {
      first = getChar();
      if (first != '"') return None();

      loop {
        c = getChar();
        if (c == '\\') {
          chars.push(c);
          continue
        };
        if (c == '\n') {
          return Some(unescape(chars.slice(0, chars.size() - 1)))
        };
        chars.push(c);
      }
    },
    _ -> return None(),
  )
}

fun getWriteKeyValueIter(
  getChar: () -> Char,
): mutable Iterator<?(String, ?String)> {
  loop {
    (key, isFinished) = getWriteKey(getChar);
    key match {
    | Some(k) -> yield (Some((k, getWriteValue(getChar))))
    | None() if (!isFinished) -> yield None()
    | None() -> void
    };
    if (isFinished) {
      break void;
    }
  }
}

base class KVStreamOp {
  children =
  | KVSep()
  | KVAcc(keyName: String, valueName: ?String)
}

fun getWriteStreamOpIter(getChar: () -> Char): mutable Iterator<?KVStreamOp> {
  lastKeyOpt: ?String = None();

  for (currentOpt in getWriteKeyValueIter(getChar)) {
    currentOpt match {
    | None() ->
      !lastKeyOpt = None();
      yield None()
    | Some(current) ->
      if (current.i0 == "") {
        yield Some(KVSep());
      } else {
        lastKeyOpt match {
        | None() -> yield Some(KVAcc(current.i0, current.i1))
        | Some(lastKey) ->
          if (current.i0 != lastKey) {
            yield Some(KVSep())
          };
          yield Some(KVAcc(current.i0, current.i1))
        }
      };
      !lastKeyOpt = Some(current.i0)
    }
  };

  yield Some(KVSep())
}

fun getWriteKeyValuesIter(
  getChar: () -> Char,
): mutable Iterator<(String, Array<String>)> {
  keyOpt: ?String = Some("");
  values = mutable Vector[];
  for (opOpt in getWriteStreamOpIter(getChar)) {
    opOpt match {
    | None() ->
      values.clear();
      !keyOpt = None()
    | Some(op) ->
      op match {
      | KVSep() ->
        keyOpt match {
        | None() -> void
        | Some(key) -> yield (key, values.toArray())
        };
        values.clear()
      | KVAcc(k, value) ->
        !keyOpt = Some(k);
        value match {
        | None() -> values.clear()
        | Some(v) -> values.push(v)
        }
      }
    }
  }
}

fun writeFromStdin(context: mutable Context, dirNameStr: String): void {
  dirName = SKFS.DirName::create(dirNameStr);
  dir = context.unsafeGetEagerDir(dirName);
  dir.writeArrayMany(
    context,
    getWriteKeyValuesIter(getChar).map(kv ->
      (
        (SKFS.SID(kv.i0) : SKFS.BaseName),
        kv.i1.map(x ~> (SKFS.StringFile(x) : SKFS.File)),
      )
    ),
  );
}

module end;

module SKFSTest;

fun insertSpecialChars(rand: mutable Random, prob: Int, input: String): String {
  chars = input.chars();
  result = mutable Vector[];

  for (i in Range(0, chars.size())) {
    if (rand.random(0, 100) < prob) {
      rand.random(0, 3) match {
      | 0 -> result.push('\\')
      | 1 -> result.push('\t')
      | 2 -> result.push('\n')
      | _ -> invariant_violation("Internal error")
      }
    };
    result.push(chars[i]);
  };

  String::fromChars(result.toArray())
}

// Tests that we can interrupt a chunk anywhere, restart with the same chunk and
// the result is the same.
fun testWriteChunk(): mutable SKFS.Context {
  nbrEntries = 5;
  maxWidth = 4;
  maxValue = 100;
  nbrRetries = 10;
  nbrRounds = 1000;
  randSeed = 23;

  rand = Random::mcreate(randSeed);
  eq = true;

  for (_round in Range(0, nbrRounds)) {
    entries = mutable Vector[];

    for (i in Range(0, nbrEntries)) {
      values = mutable Vector[];
      for (_ in Range(0, rand.random(1, 1 + maxWidth))) {
        valueStr = "v" + rand.random(0, maxValue);
        !valueStr = insertSpecialChars(rand, 10, valueStr);
        values.push(SKFS.StringFile(valueStr));
      };
      keyStr = "k" + i;
      !keyStr = insertSpecialChars(rand, 10, keyStr);
      entries.push((SKFS.SID::create(keyStr), values.toArray()));
    };

    entries.sortBy(x ~> x.i0);

    buffer = mutable Vector[];
    o = str -> buffer.push(str);

    for (kv in entries) {
      (key, values) = kv;
      for (value in values) {
        o(SKFS.escape(key.toString()));
        o("\t\"");
        o(SKFS.escape(value.value.toString()));
        o("\"\n");
      };
    };

    chunk = "\n\n" + "".join(buffer);
    chunkSize = chunk.length();
    chunksBuffer = mutable Vector[];

    for (_ in Range(0, nbrRetries)) {
      chunksBuffer.push(chunk.sub(chunk.getIter(), rand.random(0, chunkSize)));
    };

    chunksBuffer.push(chunk);

    chunks = "".join(chunksBuffer);
    chunksIter = chunks.getIter();
    next = () -> {
      chunksIter.next() match {
      | None() -> throw EndOfFile()
      | Some(c) -> c
      }
    };

    buffer.clear();

    parsedEntries = SortedMap[];
    for (kv in SKFS.getWriteKeyValuesIter(next).collect(Array)) {
      (key, values) = kv;
      !parsedEntries[key] = values;
    };

    for (key => values in parsedEntries) {
      for (value in values) {
        o(SKFS.escape(key.toString()));
        o("\t\"");
        o(SKFS.escape(value));
        o("\"\n");
      };
    };

    parsedChunk = "\n\n" + "".join(buffer);

    !eq = eq && (chunk == parsedChunk);

    if (chunk != parsedChunk) {
      break void;
    }
  };

  expectEq("Write escaping test ", eq, () -> true);

  mutable SKFS.Context{}
}
