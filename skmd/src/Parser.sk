/*****************************************************************************/
// First phase of parsing. We figure out the overall structure of the
// document, but without trying to parse within strings. That means
// that links and things like that are not parsed.
/*****************************************************************************/

module SKMD;

value class Line(indent: Int, content: Array<Char>) {
  fun drop(v: Int): this {
    Line(this.indent, this.content.drop(v))
  }
}

base class TextLine {
  children =
  | LHeader(Int, Array<Char>, Int)
  | LHtml(Array<Char>, Int)
  | LText(Line)

  fun ind(): Int
  | LHeader(_, _, i)
  | LHtml(_, i) ->
    i
  | LText(l) -> l.indent

  fun startsWith(c: Char): Bool
  | LHeader(_, t, _) -> t.maybeFirst() == Some(c)
  | LHtml _ -> false
  | LText(l) -> l.content.maybeFirst() == Some(c)

  fun isNumbered(): Bool
  | LHeader(_, t, _) ->
    t.maybeFirst().map(Chars.isDigit).default(false) &&
      t.maybeGet(1) == Some('.')
  | LHtml _ -> false
  | LText(l) ->
    l.content.maybeFirst().map(Chars.isDigit).default(false) &&
      l.content.maybeGet(1) == Some('.')

  fun toString(): String
  | LHeader(l, t, i) -> `Header[${l}]:${i} ${String::fromChars(t)}`
  | LHtml(t, i) -> `Html:${i} ${String::fromChars(t)}`
  | LText(l) -> `Text:${l.indent} ${String::fromChars(l.content)}`

  fun isEmpty(): Bool
  | LHeader _
  | LHtml _ ->
    false
  | LText(l) -> l.content.isEmpty() || l.content.all(Chars.isWhitespace)

  fun update(decr: Int, filter: Array<Char> ~> Array<Char>): this
  | LHeader(l, t, i) -> LHeader(l, filter(t), max(i - 4, 0))
  | LHtml(t, i) -> LHtml(filter(t), max(i - 4, 0))
  | LText(l) -> LText(Line(max(l.indent - 4, 0), filter(l.content)))
}

fun parseTextLines(str: mutable Iterator<Char>): mutable Iterator<TextLine> {
  indent = 0;
  loop {
    nextChar = str.next() match {
    | None() -> break void
    | Some(c) -> c
    };
    nextChar match {
    | '\n' ->
      if (indent == 0) {
        yield (LText(Line(0, Array[])));
      } else {
        i = indent;
        !indent = 0;
        yield (LText(Line(i, Array[])));
      };
      continue
    | ' ' -> !indent = indent + 1
    | '\t' -> !indent = indent + 4
    | '#' if (indent == 0) ->
      yield (parseHeader(str, 1, indent));
      !indent = 0
    | '<' if (indent == 0) ->
      yield (LHtml(parseUntilEOL(Some('<'), str), indent));
      !indent = 0
    | c ->
      yield (LText(Line(indent, parseUntilEOL(Some(c), str))));
      !indent = 0
    }
  }
}

fun parseHeader(
  str: mutable Iterator<Char>,
  level: Int,
  indent: Int,
): TextLine {
  str.next() match {
  | None() -> LHeader(level, Array[], indent)
  | Some('#') -> parseHeader(str, level + 1, indent)
  | Some(c) -> LHeader(level, parseUntilEOL(Some(c), str), indent)
  }
}

fun parseUntilEOL(first: ?Char, str: mutable Iterator<Char>): Array<Char> {
  acc = mutable Vector[];
  first match {
  | None() -> void
  | Some(c) -> acc.push(c)
  };
  loop {
    nextChar = str.next() match {
    | None() -> return acc.toArray()
    | Some(c) -> c
    };
    nextChar match {
    | '\n' -> return acc.toArray()
    | '\t' ->
      acc.push(' ');
      acc.push(' ');
      acc.push(' ');
      acc.push(' ')
    | c -> acc.push(c)
    }
  }
}

/*****************************************************************************/
/* Phase 2, we put together multi-line things. */
/*****************************************************************************/

// Intermediate elements
base class IElement {
  children =
  | IHtml(Array<Char>, Int)
  | IHeader(Int, Array<Char>, Int)
  | IBlock(BlockKind, Array<Line>)
}

base class Element {
  children =
  | Header(Int, Array<TextElement>, indent: Int)
  | Html(Array<TextElement>, indent: Int)
  | Block(BlockKind, lines: Array<Array<TextElement>>, indent: Int)
}

base class BlockKind uses Equality {
  children =
  | KParagraph()
  | KBullet()
  | KBlockQuotes()
  | KNumbered()
  | KCode()
}

fun parseElements(
  lines: mutable Iterator<TextLine>,
): mutable Iterator<IElement> {
  acc = mutable Vector[];
  loop {
    textLine = lines.next() match {
    | None() ->
      if (acc.size() != 0) {
        yield IBlock(KParagraph(), acc.toArray());
      };
      break void
    | Some(x) -> x
    };
    textLine match {
    | LHeader(headerKind, lineContent, indent) ->
      if (acc.size() != 0) {
        yield IBlock(KParagraph(), acc.toArray());
        acc.clear()
      };
      yield IHeader(headerKind, lineContent, indent)
    | LText(line) -> acc.push(line)
    | LHtml(lineContent, indent) -> yield IHtml(lineContent, indent)
    }
  }
}

/*****************************************************************************/
/* Parse parens and brackets. */
/*****************************************************************************/

fun lex(input: Accessor): mutable Iterator<(Char, Int)> {
  i = 0;
  loop {
    if (i >= input.size()) break void;
    c = input[i];
    if (c == '*' || c == '_' || c == '`') {
      j = i;
      loop {
        if (j >= input.size()) break void;
        if (input[j] != c) break void;
        !i = i + 1;
        !j = j + 1;
      };
      yield (c, j);
    } else {
      yield (c, 1);
      !i = i + 1;
    }
  }
}

fun parseParens(input: Accessor): Array<(Char, Array<Char>)> {
  iter = lex(input);
  acc = mutable Vector[];
  result = mutable Vector[];
  openingOpt: ?Char = None();
  flush = (c) -> {
    if (acc.size() != 0) {
      result.push((c, acc.toArray()));
      acc.clear();
    }
  };
  loop {
    loop {
      (c, _repeat) = iter.next() match {
      | None() ->
        flush(' ');
        return result.toArray()
      | Some(c) -> c
      };
      if (c == '(' || c == '[') {
        !openingOpt = Some(c);
        flush(' ');
        acc.push(c);
        break void;
      };
      if (c == '*' || c == '_' || c == '`') {
        !openingOpt = Some(c);
        flush(' ');
        acc.push(c);
        break void;
      };
      acc.push(c);
    };
    loop {
      (c, _repeat) = iter.next() match {
      | None() ->
        flush(' ');
        return result.toArray()
      | Some(c) -> c
      };
      if (c == '\\') {
        iter.next() match {
        | None() ->
          acc.push(c);
          flush(' ');
          return result.toArray()
        | Some((x, _)) ->
          acc.push(x);
          continue
        };
      };
      acc.push(c);
      opening = openingOpt.fromSome();
      if (opening == '(' && c == ')') {
        !openingOpt = None();
        flush(opening);
        break void;
      };
      if (opening == '[' && c == ']') {
        !openingOpt = None();
        flush(opening);
        break void;
      };
      if (opening == '*' && c == '*') {
        !openingOpt = None();
        flush(opening);
        break void;
      };
      if (opening == '_' && c == '_') {
        !openingOpt = None();
        flush(opening);
        break void;
      };
      if (opening == '`' && c == '`') {
        !openingOpt = None();
        flush(opening);
        break void;
      };
    }
  }
}

base class TextElement uses Equality {
  children =
  | TELink(String, String, ?String)
  | TEBold(String)
  | TEEmphasis(String)
  | TECode(String)
  | TEText(String)
}

fun makeElement(elt: (Char, Array<Char>)): TextElement {
  full = () -> String::fromChars(elt.i1);
  stripped = () -> String::fromChars(elt.i1.slice(1, elt.i1.size() - 1));
  elt.i0 match {
  | '*' if (
    elt.i1.maybeFirst() == Some('*') &&
    elt.i1.maybeLast() == Some('*')
  ) ->
    TEBold(stripped())
  | '_' if (
    elt.i1.maybeFirst() == Some('_') &&
    elt.i1.maybeLast() == Some('_')
  ) ->
    TEBold(stripped())
  | '`' if (
    elt.i1.maybeFirst() == Some('`') &&
    elt.i1.maybeLast() == Some('`')
  ) ->
    TECode(stripped())
  | '*' -> TEEmphasis(full())
  | '_' -> TEEmphasis(full())
  | '`' -> TECode(full())
  | _ -> TEText(full())
  };
}

fun splitText(input: Array<Char>): Array<TextElement> {
  splitAccessor(Accessor::fromArray(input))
}

fun splitAccessor(input: Accessor): Array<TextElement> {
  iter = parseParens(input).iterator();
  text = mutable Vector[];
  acc = mutable Vector<TextElement>[];
  curr = iter.next() match {
  | None() -> return Array[]
  | Some(elt) -> elt
  };
  push = (elt: TextElement) -> {
    if (!text.isEmpty()) {
      acc.push(TEText(String::fromChars(text.toArray())));
      text.clear();
    };
    acc.push(elt);
  };
  ignore = false;
  for (nextElt in iter) {
    if (ignore) {
      !ignore = false;
      !curr = nextElt;
      continue
    };
    if (curr.i0 == '[' && nextElt.i0 == '(') {
      link = parseLink(nextElt.i1.slice(1, nextElt.i1.size() - 1));
      push(
        TELink(
          String::fromChars(curr.i1.slice(1, curr.i1.size() - 1)),
          link.i0,
          link.i1,
        ),
      );
      !ignore = true;
    } else if (curr.i0 == '*' || curr.i0 == '_' || curr.i0 == '`') {
      push(makeElement(curr));
    } else {
      text.extend(curr.i1)
    };
    !curr = nextElt;
  };
  if (!ignore) {
    push(makeElement(curr));
  };
  acc.toArray()
}

/*****************************************************************************/
/* Parse multi-line bullet points etc ... */
/*****************************************************************************/

fun parseLink(input: Array<Char>): (String, ?String) {
  title: ?Int = None();
  end = input.size();
  iter = input.iterator();
  curr = 0;
  for (c in iter) {
    title match {
    | Some(t) if (c == '\'') ->
      return (
        String::fromChars(input.slice(0, end)),
        Some(String::fromChars(input.slice(t, curr))),
      )
    | None() if (c == '\'') ->
      !title = Some(curr + 1);
      !end = curr;
      while (end - 1 > 0 && Chars.isWhitespace(input[end - 1])) {
        !end = end - 1;
      }
    | _ -> void
    };
    !curr = curr + 1;
  };
  (
    String::fromChars(input.slice(0, end)),
    title.map(t -> String::fromChars(input.slice(t, input.size()))),
  )
}

fun parseMulti(
  elements: mutable Iterator<IElement>,
): mutable Iterator<Element> {
  for (elt in elements) {
    elt match {
    | IHeader(x, y, i) -> yield Header(x, splitText(y).collect(Array), i)
    | IHtml(y, i) -> yield Html(splitText(y).collect(Array), i)
    | IBlock(blockKind, arr) ->
      invariant(blockKind == KParagraph());
      parser = mutable ParseMulti();
      for (x in parser.go(arr)) {
        yield x
      }
    }
  }
}

mutable class ParseMulti(
  mutable mode: (BlockKind, Int) = (KParagraph(), 0),
  acc: mutable Vector<Array<TextElement>> = mutable Vector[],
  elements: mutable Vector<Element> = mutable Vector[],
) {
  private mutable fun pushLine(line: Line): void {
    if (line.content.size() == 0) return void;
    this.acc.push(splitText(line.content).collect(Array));
  }

  private mutable fun flush(): void {
    if (this.acc.isEmpty() && this.mode.i0 == KParagraph()) return void;
    this.elements.push(Block(this.mode.i0, this.acc.toArray(), this.mode.i1));
    this.acc.clear();
  }

  private mutable fun changeMode(
    mode: BlockKind,
    line: Line,
    toRemove: Int,
    forceFlush: Bool = false,
  ): void {
    if (toRemove != 0) {
      !line.content = line.content.slice(toRemove, Int::max);
    };
    trimmed = mutable Vector[];
    lineIter = line.content.iterator();
    for (c in lineIter) {
      if (c != ' ') {
        trimmed.push(c);
        break void;
      }
    };
    for (c in lineIter) {
      trimmed.push(c);
    };
    !line.content = trimmed.toArray();
    if (mode == this.mode.i0 && !forceFlush) {
      this.pushLine(line);
      return void;
    };
    this.flush();
    this.pushLine(line);
    this.!mode = (mode, line.indent);
  }

  mutable fun go(lines: Array<Line>): Array<Element> {
    for (line in lines) {
      iter = line.content.iterator();
      firstChar = iter.next() match {
      | None() ->
        if (line.indent == 0) {
          this.changeMode(KParagraph(), line, 0, true)
        } else {
          this.pushLine(line);
        };
        continue
      | Some(c) -> c
      };
      firstChar match {
      | '*' ->
        if (iter.next() == Some('*')) {
          void
        } else {
          this.changeMode(KBullet(), line, 1, true)
        }
      | '>' if (line.indent == 0) -> this.changeMode(KBlockQuotes(), line, 1)
      | x if (Chars.isDigit(x)) ->
        if (iter.next() is Some('.')) {
          this.changeMode(KNumbered(), line, 2)
        }
      | '`' if (line.indent == 0) ->
        if (iter.next() == Some('`')) {
          if (iter.next() == Some('`')) {
            this.changeMode(KCode(), line, 3)
          } else {
            void
          }
        } else {
          void
        }
      | _ if (
        line.indent >= 4 &&
        this.mode.i0 != KNumbered() &&
        this.mode.i0 != KBullet()
      ) ->
        this.changeMode(KCode(), line, 0)
      | _ -> this.pushLine(line)
      }
    };
    this.flush();
    this.elements.toArray()
  }
}

/*****************************************************************************/
/* Let's put it all together */
/*****************************************************************************/

fun parse(str: String): mutable Iterator<Element> {
  parseMulti(parseElements(parseTextLines(str.getIter())));
}

module end;
