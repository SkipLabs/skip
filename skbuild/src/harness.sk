module Skbuild;

const kBUFFER_LEN: Int = 256 * 1024;

class Env(
  refDir: String,
  outDir: String,
  name: String,
  profile: String,
  optTarget: ?String,
  test: Bool,
  verbose: Bool,
) {
  static fun create(): Env {
    Env(
      Environ.var("PCK_DIR"),
      Environ.var("OUT_DIR"),
      Environ.var("NAME"),
      Environ.var("PROFILE"),
      Environ.varOpt("TARGET"),
      Environ.var("TEST").lowercase() == "true",
      Environ.var("VERBOSE").lowercase() == "true",
    )
  }

  fun isWasm32(): Bool {
    this.optTarget.map(t -> t.startsWith("wasm32")).default(false)
  }

  fun resolve(path: String): String {
    Path.resolve(this.refDir, path)
  }

  fun outpath(path: String): String {
    Path.join(this.outDir, path)
  }

  fun relative(path: String): String {
    relative(path, this.refDir).fromSome()
  }

  fun targetPath(path: String, ext: String): String {
    targetPath(this.outDir, this.relative(path), ext)
  }

  fun resourcePath(path: String, ext: String): String {
    targetPath(
      this.outDir,
      this.name + "_" + this.relative(path).replace("/", "_"),
      ext,
    )
  }

  fun printString(str: String): void {
    if (this.verbose) {
      print_string(str)
    };
  }

  fun printError(error: String): void {
    if (this.verbose) {
      print_error(error)
    };
  }

  fun run(args: Array<String>, inDir: ?String = None()): void {
    if (this.verbose) {
      // TODO: colors
      print_error(`>> ${args.join(" ")}`)
    };
    optCurrent = inDir.map(dir -> {
      current = Environ.current_dir();
      Environ.set_current_dir(dir);
      current
    });
    p = System.subprocess(args, this.printString, this.printError);
    optCurrent.each(c -> Environ.set_current_dir(c));
    if (!p.success()) {
      if (!this.verbose) {
        print_raw(p.stdout);
        print_error_raw(p.stderr);
      };
      throw RuntimeException(p.exitcode())
    };
  }
}

fun build(): Result<Env, IO.Error> {
  env = Env::create();
  includeDirs = Array[];
  #forEachFunction (@skbuild_include_dirs, #function, #functionName) {
    !includeDirs = includeDirs.concat(#function(env));
  };
  links = Array[];
  #forEachFunction (@skbuild_links, #function, #functionName) {
    !links = links.concat(#function(env));
  };
  sourceDirs = Array[];
  #forEachFunction (@skbuild_source_dirs, #function, #functionName) {
    !sourceDirs = sourceDirs.concat(#function(env));
  };
  preambles = Array[];
  #forEachFunction (@skbuild_preambles, #function, #functionName) {
    !preambles = preambles.concat(#function(env));
  };
  resources = Array[];
  #forEachFunction (@skbuild_resources, #function, #functionName) {
    !resources = resources.concat(#function(env));
  };
  resourceDirs = Array[];
  #forEachFunction (@skbuild_resource_dirs, #function, #functionName) {
    !resourceDirs = resourceDirs.concat(#function(env));
  };
  build_harness(
    env,
    includeDirs,
    sourceDirs,
    preambles,
    links,
    resources,
    resourceDirs,
  );
}

fun build_harness(
  env: Env,
  includeDirs: Array<String>,
  sourceDirs: Array<String>,
  preambles: Array<String>,
  links: Array<String>,
  resources: Array<String>,
  resourceDirs: Array<String>,
): Result<Env, IO.Error> {
  isWasm = env.isWasm32();
  resourceObjs = Array[];
  resourceInfos = mutable Map[];
  for (res in resources) {
    resPath = env.resolve(res);
    if (FileSystem.exists(resPath)) {
      if (!isWasm) {
        buildResource(env, resPath, false) match {
        | Failure(err) -> return Failure(err)
        | Success(object) -> !resourceObjs = resourceObjs.concat(Array[object])
        }
      } else {
        saved = saveResource(env, res, resPath);
        resourceInfos.set(saved.i0, saved.i1)
      }
    }
  };
  for (res in resourceDirs) {
    resPath = env.resolve(res);
    if (FileSystem.exists(resPath)) {
      resChecker = TimeChecker().add(resPath);
      name = Path.basename(resPath);
      archive = env.targetPath(resPath, ".tar.gz");
      if (resChecker.isYoungerThan(archive)) {
        ensureDirectory(Path.dirname(archive), env.verbose);
        env.run(
          Array["tar", "-zcvf", archive, name],
          Some(Path.dirname(resPath)),
        );
      };
      if (!isWasm) {
        buildResource(env, archive, true) match {
        | Failure(err) -> return Failure(err)
        | Success(object) -> !resourceObjs = resourceObjs.concat(Array[object])
        }
      } else {
        saved = saveResource(env, res, resPath);
        resourceInfos.set(saved.i0, saved.i1)
      };
    }
  };
  if (!resourceInfos.isEmpty()) {
    rjson = JSON.Object(resourceInfos.map((_, v) -> JSON.String(v)));
    stream = mutable Vector[];
    rjson.writeToStream(stream.push, 2);
    FileSystem.writeTextFile(env.outpath("resources.json"), stream.join(""));
  };
  includeChecker = TimeChecker();
  !includeDirs = includeDirs.map(id -> {
    if (!Path.isAbsolute(id)) {
      !id = env.resolve(id)
    };
    if (FileSystem.exists(id)) {
      !includeChecker = includeChecker.add(
        id,
        f ~> f.endsWith(".hpp") || f.endsWith(".h"),
        _f ~> false,
      )
    };
    id
  });
  libname = `lib${env.name}.${if (isWasm) "bc" else "a"}`;
  target = env.outpath(libname);
  library = buildLibrary(
    env,
    isWasm,
    sourceDirs,
    target,
    resourceObjs,
    includeDirs,
    includeChecker,
  );
  library.each(print_library);
  preambles
    .map(p -> {
      if (!Path.isAbsolute(p)) {
        !p = env.resolve(p)
      };
      p
    })
    .each(print_preamble);
  links.each(print_link);
  Success(env)
}

fun buildLibrary(
  env: Env,
  isWasm: Bool,
  sourceDirs: Array<String>,
  target: String,
  resources: Array<String>,
  includeDirs: Array<String>,
  includeChecker: TimeChecker,
): ?String {
  sources = sourceDirs.map(sd -> {
    if (!Path.isAbsolute(sd)) {
      !sd = env.resolve(sd)
    };
    FileSystem.readFilesRecursive(
      sd,
      f ~> f.endsWith(".cpp") || f.endsWith(".c"),
      _f ~> false,
    )
  });
  if (sources.isEmpty() && resources.isEmpty()) {
    return None()
  };
  sourcesTime = includeChecker;
  sources.each(sd -> sd.each(s -> !sourcesTime = sourcesTime.add(s)));
  resources.each(rcs -> !sourcesTime = sourcesTime.add(rcs));
  includeOptions = includeDirs.map(i -> `-I${i}`);
  if (sourcesTime.isYoungerThan(target)) {
    objects = Array[];
    sources.each(tsd ->
      !objects = objects.concat(
        tsd
          .map(f -> compileFile(env, isWasm, f, includeChecker, includeOptions))
          .toArray(),
      )
    );
    cmd = if (isWasm) {
      Array["llvm-link", "-o", target].concat(objects).concat(resources)
    } else {
      Array["ar", "rcs", target].concat(objects).concat(resources)
    };
    run(cmd, env.verbose);
  };
  Some(target)
}

fun buildResource(
  env: Env,
  file: String,
  relToOut: Bool,
): Result<String, IO.Error> {
  checker = TimeChecker(mtime(file));
  object = if (relToOut) mvExt(file, ".o") else env.targetPath(file, ".o");
  if (checker.isYoungerThan(object)) {
    source = if (relToOut) mvExt(file, ".c") else env.targetPath(file, ".c");
    ensureDirectory(Path.dirname(object), env.verbose);
    workDir = if (relToOut) env.outDir else env.refDir;
    relative = relative(file, workDir).fromSome();
    name = relative.replace("/", "_").replace(".", "_");
    copyBinary(name, file, source) match {
    | Failure(err) -> return Failure(err)
    | _ -> void
    };
    _ = compileFile(env, false, source, TimeChecker(), Array[], Some(object))
  };
  Success(object)
}

fun mvExt(file: String, suffix: String): String {
  dirname = Path.dirname(file);
  ext = Path.extname(file);
  basename = Path.basename(file);
  !basename = basename.take(basename.length() - ext.length());
  Path.join(dirname, `${basename}${suffix}`);
}

fun targetPath(dir: String, file: String, suffix: String): String {
  dirname = Path.dirname(file);
  ext = Path.extname(file);
  basename = Path.basename(file);
  !basename = basename.take(basename.length() - ext.length());
  Path.join(dir, Path.join(dirname, `${basename}${suffix}`));
}

fun relative(path: String, to: String): ?String {
  if (!to.endsWith("/")) {
    !to = to + "/";
  };
  if (path.startsWith(to)) {
    Some(path.sub(path.getIter().drop(to.chars().size()), path.chars().size()))
  } else {
    None()
  }
}

class RuntimeException(code: Int) extends Exception {
  fun getMessage(): String {
    `Runtime error with code ${this.code}`
  }
}

fun run(args: Array<String>, verbose: Bool): void {
  if (verbose) {
    // TODO: colors
    print_error(`>> ${args.join(" ")}`)
  };
  p = System.subprocess(args);
  if (!p.success()) {
    print_raw(p.stdout);
    print_error_raw(p.stderr);
    throw RuntimeException(p.exitcode())
  };
}

fun ensureDirectory(path: String, verbose: Bool): void {
  if (!FileSystem.exists(path)) {
    run(Array["mkdir", "-p", path], verbose)
  }
}

fun copyBinary(
  name: String,
  src: String,
  target: String,
): Result<void, IO.Error> {
  srcFile = IO.File::open(src, IO.OpenOptions{read => true});
  dstFile = IO.File::open(
    target,
    IO.OpenOptions{write => true, create => true},
  );
  _ = dstFile.write(`unsigned char ${name}[] = {`.bytes());
  count = 0;
  write = buf -> {
    strs = buf.map(byte -> {
      prefix = if (count == 0) {
        "\n  "
      } else if (count % 12 == 0) {
        ",\n  "
      } else {
        ",  "
      };
      !count = count + 1;
      `${prefix}0x${Chars.intToHexDigits(byte.toInt(), 2)}`
    });
    return dstFile.write(strs.join("").bytes());
  };
  buf = Array::mfill(kBUFFER_LEN, UInt8::truncate(0)).mbytes();
  loop {
    srcFile.read(buf) match {
    | Failure(err) -> return Failure(err)
    | Success(nbytes) if (nbytes == 0) -> break void
    | Success(nbytes) if (nbytes < kBUFFER_LEN) ->
      write(buf.slice(0, nbytes)) match {
      | Failure(err) -> return Failure(err)
      | Success _ -> void
      }
    | Success _ ->
      write(buf.slice(0)) match {
      | Failure(err) -> return Failure(err)
      | Success _ -> void
      }
    }
  };
  _ = dstFile.write(`\n};\nunsigned int ${name}_len = ${count};\n`.bytes());
  srcFile.close();
  dstFile.close();
  Success(void)
}

fun compileFile(
  env: Env,
  isWasm: Bool,
  source: String,
  includesChecker: TimeChecker,
  includes: Array<String>,
  optObject: ?String = None(),
): String {
  ext = if (isWasm) ".bc" else ".o";
  object = optObject.default(env.targetPath(source, ext));
  if (includesChecker.add(source).isYoungerThan(object)) {
    ensureDirectory(Path.dirname(object), env.verbose);
    cmd = (if (source.endsWith(".c")) Array["clang"] else {
      Array["clang++"]
    })
      .concat(
        if (env.profile == "debug") {
          Array["-O0", "-g", "-DDEBUG"]
        } else if (env.profile == "release") {
          Array["-O3", "-DRELEASE"]
        } else {
          Array["-O2", "-g", "-DDEV"]
        },
      )
      .concat(
        if (isWasm) {
          Array["-DSKIP32", "--target=wasm32", "-emit-llvm"]
        } else {
          Array["-DSKIP64"]
        },
      )
      .concat(includes)
      .concat(Array["-o", object, "-c", source]);
    run(cmd, env.verbose);
  };
  object
}

fun print_library(library: String): void {
  print_string(`skargo:library=${library}`);
}

fun print_preamble(preamble: String): void {
  print_string(`skargo:preamble=${preamble}`);
}

fun print_link(link: String): void {
  print_string(`skargo:link=${link}`);
}

module end;
